;{               HCC FIG generic 8086 FORTH}
;{ $Id$}
;{ Copyright (2000): Albert van der Horst, HCC FIG Holland by GNU Public License}
;
        PAGE  66,106
 TITLE   GENERIC FORTH FOR 8086 $Revision$
 _HEADER_ASM
_COMMENT
        A generic version of FIG-FORTH for IBM type standard PC's
                Albert van der Horst
                HCC Forth user group
                The Netherlands
                www.forth.hccnet.nl

              based on
              FIG-FORTH
   implemented by:  Charlie Krajewski
                    205 ( BIG ) Blue Rd.
                    Middletown, CT  06457

  This implementation supports only one 64k segment

  The listing has been made possible by the
  prior work of:
               Thomas Newman, Hayward, Ca.

 : other_acknowledgements
         John_Cassidy
         Kim_Harris
         George_Flammer
         Robert_D._Villwock ;
 To upgrade, modify, and understand Fig Forth, the
 value of the following book cannot be overstated:
         Systems Guide to FIG Forth
         C. H. Ting, PhD
 It is available through MVP.  See any recent issue
 of FORTH Dimensions for their ad. (DIXIT AD MDCCCCLXXX)

No one who programs with FORTH can afford to be without:
  Starting Forth
  Leo Brodie
Get it.  Available through FORTH Interest Group.
Can also be found in many book stores.
Chapter 3 serves as a guide for the EDITOR that you
will probably type in from the FIG-Forth installation
manual.

Although there is much to be said for typing in your own
listing and getting it running, there is much to be said
not typing in your own listing.  If you feel that 100+
pages of plinking is nutty, contact me for availability
of a disc with source & executable files.  Obtainable at
a bargain basement price, prepare yourself for bargain
basement support.

All publications of the FORTH Interest Group are public domain.
They may be further distributed by the inclusion of this
credit notice:
               This publication has been made available by:

               FORTH Interest Group
               P.O. Box 1105
               San Carlos, Ca.  94070
_ENDCOMMENT
        PAGE
; ########################################################################################
;                       PREPARATION (no code)
; ########################################################################################
FIGREL  EQU     2       ;{ FIG RELEASE #}
FIGREV  EQU     0       ;{ FIG REVISION #}
USRVER  EQU     34      ;{ USER VERSION NUMBER, a digit now}
;
;{       PROTECTION}
CW      EQU     M4_CELLWIDTH       ;{ I.e. for the mode used in Forth, not in the bootcode.}
PMASK   EQU     0FFH    ;{ Allow to access only 256 blocks from OFFSET}
;
;{      ASCII CHARACTER EQUIVALENTS}
;
ABL     EQU     20H     ;{ SPACE}
ACR     EQU     0DH     ;{ CR}
ADOT    EQU     2EH     ;{ PERIOD}
BELL    EQU     07H     ;{ ^G}
BSIN    EQU     08H     ;{ INPUT DELETE CHARACTER}
BSOUT   EQU     08H     ;{ OUTPUT BACKSPACE ( ^H )}
LF      EQU     0AH     ;{ LINE FEED, USED INTERNALLY AS}
                        ;{ LINE ENDER}
FF      EQU     0CH     ;{ FORM FEED}
;
;{      MEMORY + I/O CONSTANTS}
;
NBUF    EQU     2       ;{ NO. OF BUFFERS AKA SCREENS }
KBBUF   EQU     1024    ;{DATA BYTES PER DISK BUFFER}
US      EQU     100H     ;{ USER VARIABLE SPACE}
_BITS16_1_({ RTS     EQU     0A0H    ;{ RETURN STACK & TERM BUFFER} })
_BITS32_1_({ RTS     EQU  10000H    ;{ RETURN STACK & TERM BUFFER} })
;
_EQULAYOUT_1_({
EM      EQU     M4_EM   ;{ END OF MEMORY + 1}
_HIGH_BUF_1_({
BUF1    EQU     EM-(KBBUF+2*2)*NBUF      ;{ FIRST DISK BUFFER}
STRUSA  EQU     BUF1-US         ;{ User area}
});_END___1__({ _HIGH_BUF_1_})
_LOW_BUF_1_({
STRUSA  EQU     EM-US         ;{ User area at end }
});_END___1__({ _LOW_BUF_1_})

STRTIB  EQU     STRUSA-RTS      ;{ Start return stack area}
                                ;{ Under this : data stack}
INITR0  EQU     STRUSA         ;{ Grows down}
INITS0  EQU     STRTIB          ;{ Growns down}
});_END___1__({ _EQULAYOUT_1_})

_CLASSIC_1_({
DRIVE   EQU     0       ;{ Use floppy A for blocks.}
});_END___1__({_CLASSIC_1_})

BPS     EQU     512             ;{Bytes/sector, common to all of MSDOS}
SPB     EQU     KBBUF/BPS
;
;{ PHYSICAL DISK PARAMETERS}
;{ (not needed for MODERN)}
;{ (not needed for BOOTHD)}
;
;{ Disk parameters: }
;{ HD drive 3" }
TRKS    EQU     80    ;{Number of tracks}
SPT     EQU     18    ;{Sectors/track}
HEADS   EQU     2     ;{Number of heads }
NFAT    EQU     2     ;{ Number of FATS}
SECROOT EQU     0EH   ;{ Sectors for root directory entry.}
SECFAT  EQU     9     ;{ Sectors per FAT}
MEDIA   EQU    0F0H   ;{ Descriptor byte. Anachronism.}

_COMMENT
;{ HD drive 5" }
TRKS    EQU     80      ;{Number of tracks}
SPT     EQU     15      ;{Sectors/track}
HEADS   EQU     2       ;{Number of heads }
NFAT    EQU     2       ;{ Number of FATS}
SECROOT EQU     ?       ;{ Sectors for root directory entry.}
SECFAT  EQU     ?       ;{ Sectors per FAT}
MEDIA   EQU    F0H      ;{ Descriptor byte. Anachronism.}
_ENDCOMMENT

;{ Bios specific equates.}
BOOTADDRESS     EQU     M4_BIOSBOOT ;{ PC jumps to 0:7C00 to boot}
SPDRV   EQU     HEADS*TRKS*SPT    ;{ sectors/drive}
        ;{ Skip boot sector,fats and root dir and first sector of file.}
SECSTRT EQU     1+NFAT*SECFAT + SECROOT + 1
_COMMENT
;{ Alternative if the disk need not be recognized by MSDOS}
;{ Usable for generating a bootable floppy simple.}
SECSTRT EQU     1
_ENDCOMMENT
;{ END  OF PHYSICAL DISK PARAMETERS}

_SWITCH_1_({
;{ Segments   @ Valid in real mode % Valid in protected mode }
;{ Names starting in A_ are linear, physical (32 bit), absolute addresses }
RSTSIZE     EQU     10000H  ;{ For real mode stack. }
GDTSIZE         EQU     8000H   ;{ For GDT-table.}
IDTSIZE         EQU     0800H   ;{ For IDT-table. Not yet used. }
A_FORTH0     EQU     M4_LOADADDRESS - M4_ORG ; { Physical address of Forth's CS:0 = SS:0 = ES:0 . }

A_SWITCH        EQU     M4_SWITCHORG
A_RST       EQU     A_SWITCH + 10000H
A_GDT             EQU     A_RST + RSTSIZE
A_IDT             EQU     A_GDT + GDTSIZE  ; { reserved but not yet used. }
;A_LOWDP         EQU     A_IDT + IDTSIZE ; { Must become this. }
 A_LOWDP         EQU     A_GDT + IDTSIZE 

;{ @ Real mode place for the stack.}
;{ This is such that after switching to real mode an isolated }    
;{ Stack is available }
SS_RST      EQU     A_RST/10H ;
SWITCHSEGMENT     EQU     A_SWITCH/10H ;{ @ DS and CS for real code }
; {Add this to go from GDT_CS addresses to GDT_SWITCH addresses.}
M4_SWITCHOFFSET EQU  ( A_FORTH0 - M4_SWITCHORG)

;{ The GDT_.. are offsets in the GDT table. They can be arbitrarily choosen }
;{ as far as the GDT goes as long as they are a multiple of 08H }
;{ Switching sometimes restricts these to a particular value.}
GDT_SWITCH       EQU    SWITCHSEGMENT  ;{ % Switching segment, must be same for switching to work! }
GDT_CS EQU 10H  ;{ % The protected mode code segment }
GDT_SS16 EQU 10H  ;{ % To access the real mode stack segment from protected. }
; { In fact we set the DS immediately after switching.}
GDT_SS         EQU     SS_RST ;{ % The protected mode data segment}
GDT_DS         EQU     SS_RST ;{ % The protected mode data segment}
GDT_SEGMENT    EQU     A_GDT/10H       ;{ @ General descriptor table.}

IDENTIFY_16 EQU 008FH   ;{ Identification of 16 bit data segment, byte 6}
IDENTIFY_32 EQU 00CFH   ;{ Identification of 32 bit data segment, byte 6}
IDENTIFY_INT EQU 8E00H  ;{ Identification of an interrupt descriptor, byte 5}
IDENTIFY_XR  EQU 9A00H  ;{ Identification of a code segment, execute read, byte 5}
IDENTIFY_RW  EQU 9200H  ;{ Identification of a data segment, read write, byte 5}

GDTLEN EQU GDTSIZE-1      ; {Intel peculiarity.}
BOOTOFFSET EQU 0
});_END___1__({ _SWITCH_1_})

_LINUX_N_1_({
include(constant.m4)
SIZE_TERMIO     EQU     60      ; sizeof(termio) also captured from c.
RAWIO           EQU     (ECHO | ICANON)
        global  _start        ; Entry point
});_END___1__({_LINUX_N_1_})

_LINUX_C_1_({
        extern  c_type,c_expec,c_key,c_qterm
        extern  c_rslw, c_block_exit, c_block_init
        global  figforth,_start        ; Called from c. Remainder c-routines to be called from here.
});_END___1__({_LINUX_C_1_})

        PAGE
; ########################################################################################
;                      BOOTCODE    (optional, always real mode)
; ########################################################################################

; {All bootcode must be relocatable and its memory references absolute.}
; {Not for the sake of booting, but to allow MSDOS to start the program too. }

        CSEG    SEGMENT PARA PUBLIC 'CODE'
        ASSUME CS:CSEG,DS:CSEG,SS:CSEG,ES:CSEG
    _ABSOLUTELOAD_1_({ ORG     M4_ORG})
ORG0:

_BOOTFD__1_({
        JMP     SHORT BOOT
        NOP
        ;{ MSDOS programmers reference (thru 6, 3.9)}
        DB    "DFW--EXP"
LBPS    DW         BPS
        DB         1
RESSECTORS  DW     01H
        DB         NFAT
        DW BPS*SECROOT/32
        DW         SPDRV
        DB         MEDIA
        DB         SECFAT, 0H
LSPT    DW         SPT
LHEADS  DW       HEADS
HIDDENSECS    DD        0H
HUGESECS      DD         000H
      ;{ BIOS parameter block ends here}
        DB 000H, 000H, 029H                  ;{ Required magic.}
        DB         004H, 01CH, 040H,  00BH
        DB    "           "
        DB    "FAT12   "

;{       Read the sector with number in CX (Counting from 0) to ES:BX.}
;{       Keep BX, CX  }
READSECTOR:
        PUSH    CX
        PUSH    BX
        MOV     AX,CX
        MOV     CL,SPT
        DIV     CL
        MOV     CL,AH
        INC     CL      ;{ Sectors counting from 1!}
        XOR     AH,AH   ; {Rid of remainder}
        MOV     CH,HEADS
        DIV     CH
        MOV     DH,AH   ;{ Head number}
        MOV     CH,AL   ;{ Only small disks <256 cylinders}
        MOV     DL,00            ;{Drive 0 (floppy 1)}
        MOV     AX,0201H   ;{ Read absolute one sector}
        INT(13H)                 ;{BIOS disk read function}
        POP     BX
        POP     CX
        RET

RETRY:
        CALL    DISPLAYW
        XOR     AX,AX   ;{ Reset}
        MOV     AL,' '
        MOV     DL,00            ;{Drive 0 (floppy 1)}
        INT(13H)                 ;{BIOS disk read function}
        CALL    DISPLAYW
        MOV     AL,' '
        CALL DISPLAY
        MOV     AX,CX
        CALL    DISPLAYW
        MOV     AL,' '
        CALL DISPLAY
BOOT:
        MOV     AL,'D'
        CALL    DISPLAY
        MOV AX,CS
        AND AX,AX       ;{ Z = BOOTING ?}
        JNZ  ELSE3

        MOV     AL,'F'
        CALL    DISPLAY
        MOV     AH,00   ;{ Reset}
        MOV     DL,00            ;{Drive 0 (floppy 1)}
        INT(13H)                 ;{BIOS disk read function}
        JB      RETRY

        ;{ The first file copied to a freshly formatted floppy will}
        ;{ be at SECSTRT (See also genboot.bat)}
        MOV     CX,SECSTRT      ; {Counting from zero}
        MOV     AX,BOOTADDRESS/10H ; {Bootsegment}
        MOV     ES,AX
        MOV     BX,BPS
BEGIN1: CALL    READSECTOR
        INC     CX
        ADD     BX,BPS
        JB      RETRY
        CMP     BX,RELATIVE_WRT_ORIG(TEXTEND)
        JB      BEGIN1

        MOV     AL,'W'
        CALL    DISPLAY
        CALL    DISPLAYCR
        JMP     ENDBOOT
});_END___1__({ _BOOTFD__1_})

_BOOTHD__1_({
        JMP     SHORT BOOT
        NOP
        ;{ MSDOS programmers reference (thru 6, 3.9)}
        DB    "DFW---HD"
LBPS    DW         BPS
        DB         1
RESSECTORS  DW     01H
        DB         NFAT
        DW BPS*SECROOT/32
        DW         SPDRV
        DB         MEDIA
        DB         SECFAT, 0H
        DW         SPT
        DW       HEADS
HIDDENSECS    DD        0H
HUGESECS      DD         000H
      ;{ BIOS parameter block ends here}
        DB 000H, 000H, 029H                  ;{ Required magic.}
        DB         004H, 01CH, 040H,  00BH
        DB    "           "
        DB    "RAW     "

BOOTLBA:  DB      10H, 0
        DW      (RELATIVE_WRT_ORIG(TEXTEND))/BPS      ;{ ROUNDS DOWN, BUT WE ALREADY HAVE 1 SEC.}
        DW      BPS             ;{ OFFSET AND SEGMENT}
        DW      SWITCHSEGMENT
        ;{ At a hard disk we just read sector from sector 1        }
        DD      1       ;{ Start}
        DD      0       ;{ M.S. 32 BITS}

HDREAD:
        MOV     AX,SWITCHSEGMENT
        MOV     DS,AX
        MOV     SI,RELATIVE_WRT_ORIG(BOOTLBA) ; {DS:SI absolute address of lba.}
        MOV     AX,4200H  ;{   Read extended                                }
        MOV     DX,0080H  ;{   Disk C                                 }
        INT     13H
        RET
RETRY:
        CALL    DISPLAYW
        MOV     AL,' '
        MOV     AH,00   ;{ Reset}
        MOV     DL,80            ;{Drive 80 (hd C:)}
        INT(13H)                 ;{BIOS disk read function}
        CALL    DISPLAYW
        MOV     AL,' '
        CALL DISPLAY
        MOV     AX,CX
        CALL    DISPLAYW
        MOV     AL,' '
        CALL DISPLAY
BOOT:
        MOV     AL,'D'
        CALL    DISPLAY
        MOV AX,CS
        AND AX,AX       ;{ Z = BOOTING ?}
        JNZ  ELSE3
        MOV     AL,'F'
        CALL    DISPLAY
        MOV     AH,00   ;{ Reset}
        MOV     DL,80            ;{Drive 80 (hd C:)}
        INT(13H)                 ;{BIOS disk read function}
        JB      RETRY
        CALL    HDREAD
        JB      RETRY
        MOV     AL,'W'
        CALL    DISPLAY
        CALL    DISPLAYCR
        JMP     ENDBOOT
});_END___1__({ _BOOTHD__1_})

ELSE3:
_ABSOLUTELOAD_1_({
;{ Apparently we may have to move the code, e.g. if started from MSDOS.}
;{ Prepare return to MSDOS using the original code segment.}
        MOV     AX,CS
        MOV     DS,AX
        CALL    HERE1
HERE1:  POP     BX
        MOV     CX,BX
        ADD     BX,RETDOSV-HERE1+1        ;{ Independant of load address.}
        ADD     CX,RETDOS-HERE1
        MOV     [BX],CX
        INC     BX
        INC     BX
        MOV     [BX],AX
        JMP ENDBOOT     ; {Skip booting }
});_END___1__({ _ABSOLUTELOAD_1_})

_BOOTED_1_({
; Debug code, could be dispensed with in an ideal world.
DISPLAYCR:
        MOV     AL,ACR
        CALL    DISPLAY
        MOV     AL,LF
        JMP DISPLAY

DISPLAYPC:      POP     AX
        PUSH    AX
DISPLAYW:       PUSH    AX              ;{ Display AX in hex }
        MOV     AL,AH
        CALL    DISPLAYHEX
        POP     AX
        ;{ CALL DISPLAYHEX ; RET}
DISPLAYHEX:     PUSH    AX              ;{ Display AL in hex }
        MOV     CL,4
        SAR     AL,CL
        CALL    DISPLAYHD
        POP     AX
        ;{ CALL DISPLAYHD ; RET}
DISPLAYHD:      AND     AL,0FH          ;{ Display AL as one hex digit}
        DAA
        MOV     AH,AL
        MOV     CL,5
        SHR     AH,CL
        ADC     AL,30H
        ;{ CALL DISPLAY ; RET}
DISPLAY:XOR     BH,BH           ;{ Display AL as an ASCII char}
        MOV     AH,0EH
        INT(10H)
        RET
});_END___1__({ _BOOTED_1_})

_ABSOLUTELOAD_1_({
;{ Returns to DOS, provided we started from dos as a .COM.}
;{ Use far jump restoring CS to .COM value. }
RETDOS:
        MOV     AX,CS
        MOV     DS,AX
        MOV     ES,AX
        MOV     SS,AX
        MOV     AH,4CH
        INT     21H    ;{ Only works if cs is the same as while starting.}
});_END___1__({ _ABSOLUTELOAD_1_})

ENDBOOT:

; ########################################################################################
;                       ADJUST CODE SEGMENT REGISTER (still real mode)
; ########################################################################################
;{ Required start of .COM program.}
    _HOSTED_MSDOS_1_(_REAL_1_({ORG     100H
ORIG:                         ; {Accommodate also .exe files} }))

; ########################################################################################
;                       MOVE CODE TO ITS PLACE (still real mode)
; ########################################################################################

_ABSOLUTELOAD_1_({
; {  Take care of the situation where booting code is actually started up by  }
; {  MSDOS. This is no problem as long as the code is moved to where it would }
; {  be if booted. If the code is at its place, nothing really happens here.  }
; {  Furthermore all protected code started by MSDOS must be at an absolute address.}
        STD     ; Start at the end going back.
        MOV     CX,TEXTEND-HERE5 ; Amount to move
        CALL  HERE6
HERE6:  POP     AX                 ; { Calculate address of the first byte to move}
        ADD     AX,TEXTEND-HERE6-1
        MOV     SI,AX           ; {Relocatable address, w.r.t code segment.}
        MOV     AX,CS
        MOV     DS,AX
        MOV     AX, A_FORTH0/10H ; Destination segment
        MOV     ES, AX
        MOV     DI, TEXTEND-1 
        REPNZ
        MOVSB
        PUSH    ES    ; {Corrected code segment}
        MOV BX, HERE5 
        PUSH BX     ; {Correct program counter}
        RETF        ; {Returning to here5 now}
HERE5:
        MOV     AX,CS
        MOV     DS,AX
        MOV     ES,AX
        MOV     SS,AX
        CLD     ; {Reset direction to going up.}
});_END___1__({_ABSOLUTELOAD_1_})

; ########################################################################################
;                       FILL GDT AND SWITCH TO PROTECTED MODE/32 BITS (optional)
; ########################################################################################
_SWITCH_1_({
        JMP    PROTECT
GDTLOAD DW     GDTLEN
        DD     A_GDT
PROTECT:
;{Prepare. Remember STOSW uses ES:DI                              }
        MOV     AX,GDT_SEGMENT ;{ GDT segment}
        MOV     ES,AX
        MOV     DI,0
        MOV     AX,GDTLEN
        STOSW
;{ The switch segment.         }
;{ Switch between real and (16-bit) protected mode is done,}
;{ while using this segment (Relocatable code only).}
;{ GDT_SWITCH can to an extent be choosen arbitraryly,} 
;{ as long as here we ensure that the real mode address  }
;{ is equal to the protected mode address. }
;{ You can only switch while staying at the same physical address}
;{ when you are currently executing in the range GDT_SWITCH:[0:FFFFH] }
        MOV     BX,GDT_SWITCH
        MOV     DI,BX
        MOV     AX,0FFFFH
        STOSW
        SHL     BX,4     ; { Turn segment register into IP}
        MOV     AX,BX
        STOSW
        MOV     AX,IDENTIFY_XR
        STOSW
        MOV     AX,IDENTIFY_16
        STOSW
_COMMENT
;{ The protected mode view of the real stack segment. }
;{ GDT_SS16 is arbitrary.} 
        MOV     BX,GDT_SS16
        MOV     DI,BX
        MOV     AX,0FFFFH
        STOSW
        MOV     EAX,A_RST
        STOSW           ; {Only 16 bits}
        SHR     EAX,8
        ADD     AX,IDENTIFY_RW
        STOSW
        MOV     AX,IDENTIFY_16
        STOSW
_ENDCOMMENT
;{ GDT_DS/GDT_SS to an extent be choosen arbitraryly,} 
;{ The real mode view of GDT_SS is valid, isolated and reserved for real stack.}
;{ DS is reset after switching anyway.}
;{ Accomodate a 24 bit start address, a maximal limit, large pages. }
        MOV     DI,GDT_SS ;{Identical to GDT_DS}
        MOV     AX,0FFFFH
        STOSW
        MOV     EAX,A_FORTH0
        STOSW           ; {Only 16 bits}
        SHR     EAX,8
        ADD     AX,IDENTIFY_RW
        STOSW
        MOV     AX,IDENTIFY_PROT
        STOSW
;{ PREPARE-CS 16/32 BITS                                                   }
        MOV     DI,GDT_CS
        MOV     AX,0FFFFH
        STOSW
        MOV     EAX,A_FORTH0
        STOSW           ; {Only 16 bits}
        SHR     EAX,8
        ADD     AX,IDENTIFY_XR
        STOSW
        MOV     AX,IDENTIFY_PROT
        STOSW

        LGDT    [GDTLOAD]
});_END___1__({ _SWITCH_1_})

_BOOTED_1_({
        JMP     ENDREADJUST
        _NEW_ORG(01FEH)
        ;{ Signature. Last piece of boot sector. }
        DB         055H, 0AAH
ENDREADJUST:
});_END___1__({ _BOOTED_1_})

_SWITCH_1_({
;{ Remember: we are now in the real mode for a protected model.}
;{ Make sure we are in the switch segment, such that we can switch.}
        MOV   BX,CS      ;{ Reality.}
        MOV AX, GDT_SWITCH ;{ Dream.}
        PUSH AX     ; {Correct code segment}
        SUB AX,BX      ;{ Discrepancy between dream and reality}
        MOV CX,10h     ;{ How much units would that be for the IP?}
        CWD
        MUL CX
        CALL  HERE3
HERE3:  POP   BX         ;{ Reality.}
        SUB BX,AX    ;
        ADD BX,THERE4-HERE3
        PUSH BX     ; {Corrected program counter}
        RETF        ; {Returning to THERE4 now}

_BITS16_1_({THERE4:})

_BITS32_1_({
;{ 32 bit protected mode is no good unless the A20 address line works.}
;{ The following tedious code is copied from the nuni startup code}
;{ for linux. }
KB_WAIT:
        IN AL,64H
        AND AL,2
        JNZ KB_WAIT
        RET     
THERE4:
        CALL KB_WAIT           
        MOV AL,0D1H
        OUT 064H,AL            ;{ Enable a20}
        CALL KB_WAIT
        MOV AL,0DFH
        OUT 060H,AL
});_END___1__({ _BITS32_1_})
});_END___1__({ _SWITCH_1_})


;{************************}
_BITS32_1_({include(width32.m4) })
;{************************}
;{ The following had to wait until width32.m4 had been included.}
BMASK   EQU     BLOCKS_IN_USE_MASK
       
_SWITCH_1_({
        CLI     ;{ Wait for stacks to be setup.}
        JMPHERE_FROM_REAL
        MOV     AX,GDT_SS
        MOV     SS,AX
REP1:
_BITS32_1_({
        INC AX                  ;{ Wait until a20 works(!)}
        MOV [M4_LOADADDRESS],AX
        CMP AX,[100000H + M4_LOADADDRESS] ; { 2^20 beyond}
        JE REP1  
});_END___1__({ _BITS32_1_})
});_END___1__({ _SWITCH_1_})
       
; ########################################################################################
;                       FORTH GLUE CODE (optional, except for the jump)
; ########################################################################################

_HOSTED_LINUX_1_({
        section forth progbits write exec alloc
});_END___1__({ _HOSTED_LINUX_1_})

_LINUX_C_1_({
SAVE_LINUX:     DD      0,0,0,0,0 ; SP, BP, SI, DI, BX
RETURN_LINUX:
        MOV     ESP,_CELL_PTR[SAVE_LINUX+CELLS(0)]
        MOV     EBP,_CELL_PTR[SAVE_LINUX+CELLS(1)]
        MOV     ESI,_CELL_PTR[SAVE_LINUX+CELLS(2)]
        MOV     EDI,_CELL_PTR[SAVE_LINUX+CELLS(3)]
        MOV     EBX,_CELL_PTR[SAVE_LINUX+CELLS(4)]
        RET     ; Assuming the old stack has not been disturbed.It shouldn't.
figforth:
        MOV     _CELL_PTR[SAVE_LINUX+CELLS(0)],ESP
        MOV     _CELL_PTR[SAVE_LINUX+CELLS(1)],EBP
        MOV     _CELL_PTR[SAVE_LINUX+CELLS(2)],ESI
        MOV     _CELL_PTR[SAVE_LINUX+CELLS(3)],EDI
        MOV     _CELL_PTR[SAVE_LINUX+CELLS(4)],EBX
        LEA     ECX,[ESP+CELLS(1)]
        XOR     EAX,EAX
        CMP     EAX,_CELL_PTR[ECX]
        JZ     ENDIF1
        JMP     BOOTUP+CELLS(2)         ; Warm start
ENDIF1:

});_END___1__({_LINUX_C_1_})

_LINUX_N_1_({
figforth:
_start:
});_END___1__({_LINUX_N_1_})

        JMP     BOOTUP                  ; Cold start

; ########################################################################################
;                       FORTH ITSELF (entry point : BOOTUP)
; ########################################################################################
;
_COMMENT
   FORTH REGISTERS

   FORTH   8088     FORTH PRESERVATION RULES
   -----   ----     ----- ------------ -----
    IP      SI      Interpreter pointer.  Must be preserved
                    across FORTH words.

     W      BX      Working register.  When entering a word
                    via its code field the CFA is passed in BX.

    SP      SP      Parameter stack pointer.  Must be preserved
                    across FORTH words.

    RP      BP      Return stack.  Must be preserved across
                    FORTH words.

            AX      General register.  Used to pass data from
                    FORTH words, see label APUSH.

            DX      General register.  Used to pass more data from
                    FORTH words, see label DPUSH.

            BX      General purpose register.

            CX      General purpose register.

            CS      Segment register.  Must be preserved
                    across FORTH words.

            DS      ditto

            SS      ibid

            ES      Temporary segment register only used by
                    a few words. However it MUST remain equal to
                    DS, such that string primitives can be used
                    with impunity.

----------------------------------------------------------
_ENDCOMMENT
        PAGE
_COMMENT
---------------------------------------------

   COMMENT CONVENTIONS
   ------- -----------

   =       IS EQUAL TO
   <-      ASSIGNMENT

  NAME        =  Address of name
  (NAME)      =  Contents of name
  ((NAME))    =  Indirect contents

  CFA         =  Address of CODE FIELD
  LFA         =  Address of LINK FIELD
  NFA         =  Address of NAME FIELD
  PFA         =  Address of PARAMETER FIELD

  S1          =  Parameter stack - 1st word
  S2          =  Parameter stack - 2nd word
  R1          =  Return stack    - 1st word
  R2          =  Return stack    - 2nd word

  LSB         =  Least significant bit
  MSB         =  Most  significant bit
  LB          =  Low byte
  HB          =  High byte
  LW          =  Low  word

------------------------------------------------------------
_ENDCOMMENT
_OLDDEBUG_1_({
        PAGE
_COMMENT
             DEBUG SUPPORT

THIS ROUTINE WILL ALLOW YOU TO STEP THRU FORTH PROGRAMS
EVERY TIME 'NEXT' IS EXECUTED.

IN ORDER TO USE THE STEP FEATURE YOU MUST DO THE FOLLOWING:

        1.  PATCH THE INSTRUCTION IN 'NEXT' WITH A JUMP
            TO 'TNEXT'

        2.  PATCH YOUR BREAKPOINT ROUTINE AT
            LABEL   'BREAK'

        3.  SET VARIABLES, `BIP' & `BIPE' TO THE
            ADDRESSES YOU WANT TO STEP THRU.

THE CONTENTS OF THE 2 VARIABLES 'BIP` AND `BIPE'
ARE INTERPRETED AS FOLLOWS:

BIP     BIPE    DEBUG-CONDITION
---     ----    ---------------

  0        X    OFF
 -1        X    TRACE ALL `NEXT' CALLS
ADDR1      0    TRACE `ADDR1' ONLY
ADDR1  ADDR2    TRACE `ADDR1' TO `ADDR1'

NOTE:   THE ABOVE ADDRESSES CAN'T POINT TO A
        `CODE FIELD ADDRESS'.
        X = DON'T CARE

-----------------------------------------------------
_ENDCOMMENT

BIP     DC      0       ;{ BREAKPOINT START ADDRESS}
BIPE    DC      0       ;{ BREAKPOINT END ADDR}
        PAGE
;{      THIS IS THE `NEXT' WITH DEBUG SUPPORT}

TNEXT:  PUSHF           ;{SAVE REGISTER}
        PUSH    AX
        MOV     AX,[BIP]  ;{ BREAKPOINT START ADDR}
        OR      AX,AX   ;{ ZERO?}
        JZ      TNEXT2  ;{ NO BREAKPOINT}
        CMP     AX,-1
        JZ      TNEXT1  ;{ STEP ALL POINTS}
        CMP     AX,SI   ;{ IN BREAKPOINT RANGE?}
        JZ      TNEXT1  ;{ STEP THIS LOCATION}
        JA      TNEXT2  ;{ NO}
        MOV     AX,[BIPE] ;{ BREAKPOINT END ADDR}
        OR      AX,AX   ;{ ZERO?}
        JZ      TNEXT2  ;{ ONLY 1 LOCATION}
        CMP     AX,SI   ;{ IN RANGE STILL?}
        JB      TNEXT2  ;{ NO}

;{      PAUSE ON ADDRESS}
;
TNEXT1: POPF
;
;{********  ADD YOUR BREAKPOINT HERE  **********}
;
BREAK:  JMP     SHORT   TNEXT3  ;{CONT WITH PROGRAM}
;
;{      NO BREAKPOINT PAUSE - RESTORE REGISTERS}
;
TNEXT2: POP     AX
        POPF
TNEXT3: LODSW           ;{ AX <- (IP)}
        MOV     BX,AX
        JMP     SHORT   NEXT1
});_END___1__({_OLDDEBUG_1_ })
        PAGE
_NEWDEBUG_1_({
DISPLAYSI:
        _SWITCH_1_({JMPHERE_FROM_FORTH})
        SET_16_BIT_MODE
        PUSH    SI
        PUSH    BP
        PUSH    BX
        MOV     AX,BX
        CALL    DISPLAYW
        MOV     AX,' '
        CALL    DISPLAY
        MOV     AX,SI
        CALL    DISPLAYW
        CALL    DISPLAYCR
        _BITS32_1_({SET_32_BIT_MODE})
        MOV     AH,10H
        INT     16H
        _SWITCH_1_({JMPHERE_FROM_OS})
        POP    BX
        POP    BP
        POP    SI
        RET
});_END___1__({_NEWDEBUG_1_ })

DPUSH:  PUSH    DX
APUSH:  PUSH    AX

_OLDDEBUG_1_({
_COMMENT

    Patch the next 3 locations ( using a DBUG monitor ) with a
    `JMP TNEXT' for tracing through high level FORTH words.

_ENDCOMMENT
});_END___1__({_OLDDEBUG_1_ })

;{ In 32 bit versions there may be no jumps to NEXT at all }
;{ The label NEXT1 is rarely relevant (for _OLDDEBUG_1_) }
NEXT:   LODSW           ;{AX <- (IP)}
NEXT1:  MOV     BX,AX   ;{ (W) <- (IP)}
_NEWDEBUG_1_({ CALL    DISPLAYSI})
        JMP     _CELL_PTR[BX]    ;{ TO `CFA'}
        PAGE
;
;{       Dictionary starts here.}
DP0:
CODE_HEADER({LIT},{LIT})
        LODSW           ;{ AX <- LITERAL}
        _PUSH          ;{ TO TOP OF STACK}
;
CODE_HEADER({EXECUTE},{EXEC})
        POP     BX      ;{ GET CFA}
        JMP     _CELL_PTR[BX]
;
worddoc({CONTROL},{BRANCH},{branch},{},{C2,L0},{
The run-time proceedure to unconditionally branch. An in-line offset
is added to the interpretive pointer forthcode({IP}) to branch ahead or back.
forthcode({BRANCH}) is compiled by forthcode({ELSE}) , forthcode({AGAIN}) , forthcode({REPEAT}) .},{})
CODE_HEADER({BRANCH},{BRAN})
BRAN1:  ADD     SI,[SI]
        _NEXT    ;{ JUMP TO OFFSET}
;
worddoc({CONTROL},{0BRANCH},{zero_branch},{f ---},{C2},{
The run-time proceedure to conditionally branch. If forthvar({f}) is false
(zero), the following in-line parameter is added to the interpretive
pointer to branch ahead or back. Compiled by forthcode({IF}) , forthcode({UNTIL}) , and forthcode({WHILE}) .
},{})
CODE_HEADER({0BRANCH},{ZBRAN})
        POP     AX      ;{ GET STACK VALUE}
        OR      AX,AX   ;{ ZERO?}
        JZ      BRAN1   ;{ YES, BRANCH}
        LEA     SI,[SI+CELLS(1)]
        _NEXT
        PAGE
HEADER({NOOP},{NOOP},{$+4})
;{      DEBUG STUFF}
NOP0:   DC      $+CELLS(1)
        JMP SHORT     NEXT
NOP1:   DC      $+CELLS(1)
        JMP SHORT     NEXT
NOP2:   DC      $+CELLS(1)
        JMP SHORT     NEXT
;
worddocsafe({CONTROL},{(LOOP)},{paren_loop},{},{C2},{
The run-time proceedure compiled by LOOP which increments the loop
index and tests for loop completion.
},{{LOOP}})
CODE_HEADER({(LOOP)},{XLOOP})
        MOV     BX,1    ;{ INCREMENT}
XLOO1:  ADD     [BP],BX ;{ INDEX = INDEX + INCR}
        MOV     AX,[BP] ;{ GET NEW INDEX}
        SUB     AX,[BP+CELLS(1)]        ;{ COMPARE WITH LIMIT}
        XOR     AX,BX   ;{ TEST SIGN}
        JS      BRAN1   ;{ KEEP LOOPING}
;
;{  END OF `DO' LOOP}
        ADD     BP,_BYTE CELLS(2)  ;{ ADJ RETURN STACK}
        LEA     SI,[SI+CELLS(1)]       ;{ BYPASS BRANCH OFFSET}
        _NEXT
;
worddoc({CONTROL},{+LOOP},{plus_loop},{n1 --- (run)  addr n2 --- (compile)},{P,C2,L0},{
Used in a colon-definition in the form:
forthsamp({DO ... n1 +LOOP})
At run-time, forthcode({+LOOP}) selectively controls branching
back to the corresponding forthcode({DO}) based on forthvar({n1}) , the loop index and the
loop limit. The signed increment forthvar({n1}) is added to the index and the
total compared to the limit.
The branch back to forthcode({DO}) occurs until the new index is equal to or
greater than the limit (forthvar({n1>0})), or until the new index is equal to or
less than the limit (forthvar({n1<0})). Upon exiting the loop, the parameters
are discarded and execution continues ahead.

At compile time, forthcode({+LOOP}) compiles the run-time word forthcode({(+LOOP)}) and the
branch offset computed from forthcode({HERE}) to the address left on the stack by
forthcode({DO}) . forthvar({n2}) is used for compile tine error checking.},{})
CODE_HEADER({(+LOOP)},{XPLOO})
        POP     BX      ;{ GET LOOP VALUE}
        JMP SHORT     XLOO1
;
worddocsafe({CONTROL},{(DO)},{paren_do},{},{C},{
The run-time proceedure compiled by forthcode({DO}) which moves the loop control
parameters to the return stack.},{{DO}})
CODE_HEADER({(DO)},{XDO})
        POP     DX      ;{ INITIAL INDEX VALUE}
        POP     AX      ;{ LIMIT VALUE}
        XCHG    BP,STACKPOINTER   ;{ GET RETURN STACK}
        PUSH    AX
        PUSH    DX
        XCHG    BP,STACKPOINTER   ;{ GET PARAMETER STACK}
        _NEXT
;
worddoc({CONTROL},{I},{i},{--- n},{C,L0},{
Used within a do-loop to copy the loop index to the stack. Other use
is implementation dependent.},{{R}})
CODE_HEADER({I},{IDO})
        MOV     AX,[BP] ;{ GET INDEX VALUE}
        _PUSH          ;{ TO PARAMETER STACK}
;
HEADER({+ORIGIN},{PORIG},{DOCOL})
        DC      LIT
        DC      USINI
        DC      PLUS
        DC      SEMIS
;
;
;{      Initialisation block for user variables through VOC-LINK}
;{       <<<<< must be in same order as user variables >>>>>}
;
BOOTUP:
        NOP                    ; Fills jump to 4 bytes (for 16 bits code)
        _BITS32_1_({NOP         ; or to 8 bytes for 32 bits code  })
        _BITS32_1_({NOP })
        JMP     LCLD     ;{VECTOR TO COLD START}
        NOP                    ; Fills jump to 4 bytes (for 16 bits code)
        _BITS32_1_({NOP         ; or to 8 bytes for 32 bits code  })
        _BITS32_1_({NOP })
        JMP     WRM     ;{ VECTOR TO WARM START}
        DB      FIGREL  ;{ FIG RELEASE #}
        DB      FIGREV  ;{ FIG REVISION #}
        DB      USRVER  ;{ USER REVISION #}
        DB      0EH     ;{ VERSION ATTRIBUTES}
        _BITS32_1_({DC 0   ; Fill version info up to two cells. })
USINI:
        DC      _NAME_FIELD(TASK) ;{ FIRST DEFINITION 0 }
        DC      STRUSA  ;{ INIT (U0) USER AREA POINTER 1}
        DC      BSIN    ;{ RUBOUT: get rid of latest char 2}
        DC      INITS0  ;{ INIT (S0)         3}
        DC      INITR0  ;{ INIT (R0)         4}
        DC      STRTIB  ;{ INIT (TIB)        5}
        DC      32      ;{ INIT (WIDTH)      6}
        DC      0       ;{ INIT (WARNING)      7}
        DC      INITDP  ;{      INIT (FENCE)  8}
        DC      INITDP  ;{      INIT (DP)     9}
        DC      _VOC_LATEST(FORTH) ;{       INIT (VOC-LINK) 10}
_LINUX_C_1_({
        DC      0       ;{ INIT (OFFSET) }
});_END___1__({_LINUX_C_1_})
_LINUX_N_1_({
        DC      0       ;{ INIT (OFFSET) }
});_END___1__({_LINUX_N_1_})
_RWFILE_1_({
        DC      0       ;{ INIT (OFFSET) }
});_END___1__({_RWFILE_1_})
_CLASSIC_1_({
_COMMENT
;{ This will steer clear of a forth executable, provided it is}
;{ the first file on the floppy. }
;{ (X-1)/S+1 rounds up to whole `S', this is used several times. }
;{ The 2 is rounding plus the extra sector in .EXE files.}
;{ However it deviates much from the classic model and requires}
;{ that the disk parameters are available (not only for USEBIOS)}
        DC      (RELATIVE_WRT_ORIG(TEXTEND)-1)/KBBUF+1 ; +1 for EXE
_ENDCOMMENT
;{ So for now just the classic, swap disk after booting.}
        DC      0       ;{ INIT (OFFSET) }
});_END___1__({_CLASSIC_1_})
_RWFD_1_({
;{ This will reserve place for dos information, such that the floppy}
;{ can be accessed from DOS and makes sure blocks are not allocated}
;{ within the forth system itself. }
;{ BPS subtracted because SECSTRT corresponds with one sector into source. }
;{ Round up both parts separately.}
;       You need a filler file of one sector,
;       if this  vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv even (for a 3" drive).
        DC      (SECSTRT+((RELATIVE_WRT_ORIG(TEXTEND)-1-BPS)/BPS+1)-1)/SPB+1     ;{ INIT (OFFSET) }
});_END___1__({_RWFD_1_})
_RWHD_1_({
;{ This will reserve place for the system itself, on the first part of a }
;{ hard disk. No problem to just reserve whole memory.}
        DC      64      ;{ Kbyte.}
});_END___1__({_RWHD_1_})
;
;
;{      The following is the CPU's name, printed}
;{       during cold start.}
;{       The name is 32 bits in base 36.}
;
_SWITCH_1_({
; We either switch to prot, run linux (always prot) or run real mode
; Be careful that these classes are mutually exclusive and cover all.
CPUNM:  DC      0CDH,1856H       ;{ '80386'     12 13 }
});_END___1__({ _SWITCH_1_})
_HOSTED_LINUX_1_({
CPUNM:  DC      0CDH,1856H       ;{ '80386'     12 13 }
});_END___1__({ _HOSTED_LINUX_1_})
_REAL_1_({
CPUNM:  DC      5H,0B328H       ;{ '8088'     12 13 }
});_END___1__({ _REAL_1_})
_SWITCH_1_({
; {Swap the following with DP to allocate in conventional memory.}
         DC     A_LOWDP ; {LOW-DP        14}
; {Leave space to start conventional programs}
         DC     80000H          ; {LOW-EM        15}
});_END___1__({ _SWITCH_1_})
;{      <<<<< end of data used by cold start >>>>>}
        _RESB(US-($ - USINI))        ;{ All user can be initialised.}
;
worddoc({NUMBER},{DIGIT},{digit},{c n1 --- n2 tf (ok) c n1 --- ff (bad)},{},{
Converts the ascii character forthvar({c}) (using base forthvar({n1}) ) to its binary
equivalent forthvar({n2}) , accompanied by a true flag. If the conversion is
invalid, leaves only a false flag.},{})
CODE_HEADER({DIGIT},{DIGIT})
        POP     DX      ;{NUMBER BASE}
        POP     AX      ;{ASCII DIGIT}
        SUB     AL,'0'
        JB      DIGI2   ;{NUMBER ERROR}
        CMP     AL,9
        JBE     DIGI1   ;{NUMBER = 0 THRU 9}
        SUB     AL,7
        CMP     AL,10   ;{NUMBER 'A' THRU 'Z'?}
        JB      DIGI2   ;{NO}
DIGI1:  CMP     AL,DL   ;{ COMPARE NUMBER TO BASE}
        JAE     DIGI2   ;{NUMBER ERROR}
        SUB     DX,DX   ;{ZERO}
        MOV     DL,AL   ;{NEW BINARY NUMBER}
        MOV     AL,1    ;{TRUE FLAG}
        _2PUSH          ;{ADD TO STACK}
;{   NUMBER ERROR}
DIGI2:  SUB     AX,AX   ;{FALSE FLAG}
        _PUSH
        PAGE
worddocsafe({DICTIONARY},{(FIND)},{paren_find},{addr1 addr2 --- pfa b tf (ok)
addr1 addr2 .--- ff (bad)},{},{
Searches the dictionary starting at the name field address forthvar({addr2}),
matching to the text at forthvar({addr1}). Returns parameter field address,
length byte of name field and boolean true for a good match. If no
match is found, only a boolean false is left.},{{FIND}})
CODE_HEADER({(FIND)},{PFIND})
;{       MOV     AX,DS}
;{       MOV    ES,AX   ;ES = DS}
        POP     BX      ;{NFA}
        POP     CX      ;{STRING ADDR}
;
;{  SEARCH LOOP}
PFIN1:  MOV     DI,CX   ;{GET ADDR}
        MOV     AL,[BX] ;{GET WORD LENGTH}
        MOV     DL,AL   ;{SAVE WORD LENGTH}
        XOR     AL,[DI]
        AND     AL,3FH  ;{CHECK LENGTHS}
        JNZ     PFIN5   ;{LENGTHS DIFFER}

;
;{   LENGTHS MATCH - CHECK EACH CHARACTER IN NAME}
PFIN2:  INC     BX
        INC     DI      ;{ NEXT CHAR OF NAME}
        MOV     AL,[BX]
        XOR     AL,[DI] ;{COMPARE NAMES}
        ADD     AL,AL   ;{THIS WILL BE TEST BIT 8}
        JNZ     PFIN5   ;{NO MATCH}
        JNB     PFIN2   ;{MATCH SO FAR - LOOP}
;
;{   FOUND END OF NAME (BIT 8 SET) - A MATCH}
        ADD     BX,_BYTE 1+CELLS(2);{ BX = PFA}
        PUSH    BX      ;{ (S3) <- PFA}
        MOV     AX,1    ;{TRUE VALUE}
        SUB     DH,DH
        _2PUSH
;
;{   NO NAME MATCH - TRY ANOTHER}
;
;{ GET NEXT LINK FIELD ADDR (LFA)}
;{ ( ZERO = FIRST WORD OF DICTIONARY )}
;
PFIN5:  INC     BX      ;{NEXT ADDR}
        JB      PFIN6   ;{END OF NAME}
        MOV     AL,[BX] ;{GET NEXT CHAR}
        ADD     AL,AL   ;{SET/RESET CARRY}
        JMP SHORT     PFIN5   ;{LOOP UNTIL FOUND}
;
PFIN6:  MOV     BX,[BX] ;{ GET LINK FIELD ADDR}
        OR      BX,BX   ;{ START OF DICT ( 0 )}
        JNZ     PFIN1   ;{ NO , LOOK MORE}
        MOV     AX,0    ;{ FALSE FLAG}
        _PUSH          ;{ DONE ( NO MATCH FOUND )}
        PAGE
CODE_HEADER({ENCLOSE},{ENCL})
        POP     AX      ;{S1 - TERMINATOR CHAR}
        POP     BX      ;{S2 - TEXT ADDR}
        PUSH    BX      ;{ADDR - BACK TO STACK ( IT RHYMES )}
        MOV     AH,0    ;{ZERO}
        MOV     DX,-1   ;{CHAR OFFSET COUNTER}
        DEC     BX      ;{ADDR -1}
;
;{   SCAN TO FIRST NON-TERMINATOR CHARACTER}
ENCL1:  INC     BX      ;{ADDR+1}
        INC     DX      ;{COUNT+1}
        CMP     AL,[BX]
        JZ      ENCL1   ;{WAIT FOR NON-TERMINATOR}
        PUSH    DX      ;{OFFSET TO 1ST TEXT CHAR}
        CMP     AH,[BX] ;{NULL CHAR?}
        JNZ     ENCL2   ;{NO}
;
;{  FOUND NULL BEFORE 1ST NON-TERM CHAR}
        MOV     AX,DX   ;{COPY COUNTER}
        INC     DX      ;{ +1}
        _2PUSH
;
;{   FOUND FIRST TEXT CHAR - COUNT THE CHARS}
ENCL2:  INC     BX      ;{ ADDR+1}
        INC     DX      ;{COUNT+1}
        CMP     AL,[BX] ;{TERMINATOR CHAR?}
        JZ      ENCL4   ;{YES}
        CMP     AH,[BX] ;{NULL CHAR?}
        JNZ     ENCL2   ;{NO, LOOP AGAIN}
;
;{   FOUND NULL AT END OF TEXT}
ENCL3:  MOV     AX,DX   ;{COUNTERS ARE EQUAL}
        _2PUSH
;
;{   FOUND TERMINATOR CHARACTER}
ENCL4:  MOV     AX,DX
        INC     AX      ;{COUNT+1}
        _2PUSH
        PAGE
        ;{ At line     LINE ~500}
HEADER({CR},{CR},{DOCOL})
        DC      LIT,LF
        DC      EMIT
        DC      SEMIS
        PAGE
CODE_HEADER({CMOVE},{LCMOVE})
        CLD             ;{INC DIRECTION}
        MOV     BX,SI   ;{SAVE IF}
        POP     CX      ;{COUNT}
        POP     DI      ;{DEST}
        POP     SI      ;{SOURCE}
;{       MOV    AX,DS}
;{       MOV    ES,AX   ;ES <- DS}
        REP     MOVSB   ;{THAT'S THE MOVE}
        MOV     SI,BX   ;{GET BACK IP}
        _NEXT
;
worddoc({MULTIPLYING},{U*},{u_start},{u1 u2 --- ud},{},{
A mixed magnitude math operation which leaves the double number
 forthvar({d}) : the unsigned product of two unsigned numbers
 forthvar({n1}) and forthvar({n2}) .},{{U/}, {M*}.{*}})
CODE_HEADER({U*},{USTAR})
        POP     AX
        POP     BX
        MUL     BX      ;{UNSIGNED}
        XCHG    AX,DX   ;{AX NOW = MSW}
        _2PUSH          ;{STORE DOUBLE CELL}
;
worddoc({MULTIPLYING},{U/},{u_slash},{ud u1 --- u2 u3},{},{
Leave the unsigned remainder forthvar({u2}) and unsigned quotient forthvar({u3}) from the
unsigned double dividend forthvar({ud}) and unsigned divisor forthvar({u1}) .},{{U*},{M/},{/}})
CODE_HEADER({U/},{USLAS})
        POP     BX      ;{DIVISOR}
        POP     DX      ;{MSW OF DIVIDEND}
        POP     AX      ;{LSW OF DIVIDEND}
        CMP     DX,BX   ;{DICIDE BY 0?}
        JNB     DZERO   ;{ ERROR - ZERO DIVIDE}
        DIV     BX      ;{16 BIT DIVIDE}
        _2PUSH          ;{STORE QUOT/REM}
;
;{      DIVIDE BY ZERO ERROR - SHOW MAX NUMBERS}
DZERO:  MOV     AX,-1
        MOV     DX,AX
        _2PUSH          ;{STORE QUOT/REM}
;
worddoc({LOGIC},{AND},{and},{n1 n2 --- n2},{L0},{
Leave the bitwise logical and of forthvar({n1}) and forthvar({n2}) as forthvar({n3}) .},{{XOR},{OR}})
CODE_HEADER({AND},{LAND})
        POP     AX
        POP     BX
        AND     AX,BX
        _PUSH
;
worddoc({LOGIC},{OR},{or},{n1 n2 --- or},{L0},{
Leave the bit-wise logical or of two l6 bit values.},{{AND},{XOR}})
CODE_HEADER({OR},{LOR})
        POP     AX      ;{ (S1) <- (S1) OR (S2)}
        POP     BX
        OR      AX,BX
        _PUSH
;
worddoc({LOGIC},{XOR},{xor},{nl n2 --- xor},{L1},{
Leave the bitwise logical exclusive or of two values.},{{AND},{OR}})
CODE_HEADER({XOR},{LXOR})
        POP     AX      ;{ (S1) <- (S1) XOR (S2)}
        POP     BX
        XOR     AX,BX
        _PUSH
        PAGE
CODE_HEADER({SP@},{SPFET})
        MOV     AX,STACKPOINTER   ;{ (S1) <- (SP)}
        _PUSH
;
CODE_HEADER({SP!},{SPSTO})
        MOV     BX,_CELL_PTR[USINI+CELLS(1)]   ;{USER VAR BASE ADDR}
        MOV     STACKPOINTER,[BX+CELLS(3)]        ;{RESET PARAM STACK POINTER}
        _NEXT
;
CODE_HEADER({RP@},{RPFET})      ;{(S1) <- (RP)}
        MOV     AX,BP   ;{RETURN STACK ADDR}
        _PUSH
;
CODE_HEADER({RP!},{RPSTO})
        MOV     BX,_CELL_PTR[USINI+CELLS(1)]   ;{(AX) <- USR VAR BASE}
        MOV     BP,[BX+CELLS(4)]        ;{RESET RETURN STACK PTR}
        _NEXT
;
worddoc({SCREEN},{;S},{semicolon_s},{},{P,L0},{
Stop interpretation of a screen.
forthcode({;S}) is also the run-time word compiled at the end of a
colon-definition which returns execution to the calling proceedure.},{})
CODE_HEADER({;S},{SEMIS})
        MOV     SI,[BP] ;{(IP) <- (R1)}
        LEA     BP,[BP+CELLS(1)]
        _NEXT
;
worddoc({CONTROL},{LEAVE},{leave},{},{C,L0},{
Force termination of a do-loop at the next opportunity by setting
the loop limit equal to the current value of the index. The index
itself remains unchanged, and execution proceeds normally until forthcode({LOOP}) 
or forthcode({+LOOP}) is encountered.},{})
CODE_HEADER({LEAVE},{LLEAV})  ;{LIMIT <- INDEX}
        MOV     AX,[BP] ;{GET INDEX}
        MOV     [BP+CELLS(1)],AX        ;{STORE IT AT LIMIT}
        _NEXT
        PAGE
;
CODE_HEADER({>R},{TOR})        ;{ (R1) <- (S1)}
        POP     BX      ;{GET STACK PARAMETER}
        LEA     BP,[BP-CELLS(1)]    ;{MOVE RETURN STACK DOWN}
        MOV     [BP],BX ;{ADD TO RETURN STACK}
        _NEXT
;
CODE_HEADER({R>},{FROMR})      ;{(S1) <- (R1)}
        MOV     AX,[BP] ;{ GET RETURN STACK VALUE}
        LEA     BP,[BP+CELLS(1)]
        _PUSH
;
HEADER({R},{RR},{IDO+CELLS(1)})
;
worddoc({LOGIC},{0=},{zero_equals},{n --- f},{L0},{
Leave a true flag forthvar({f}) is the number forthvar({n}) is equal to zero, otherwise leave a
false flag.},{{=}})               
CODE_HEADER({0=},{ZEQU})
        POP     AX
        OR      AX,AX   ;{DO TEST}
        MOV     AX,1    ;{TRUE}
        JZ      ZEQU1   ;{IT'S 0}
        DEC     AX      ;{FALSE}
ZEQU1:  _PUSH
;
worddoc({LOGIC},{0<},{zero_less},{n --- f},{L0},{
Leave a true flag if the number is less than zero (negative),
otherwise leave a false flag.},{{<}})
CODE_HEADER({0<},{ZLESS})
        POP     AX
        OR      AX,AX   ;{SET FLAGS}
        MOV     AX,1    ;{TRUE}
        JS      ZLESS1
        DEC     AX      ;{FALSE}
ZLESS1: _PUSH
;
worddoc({OPERATOR},{+},{plus},{n1 n2 --- sum},{L0},{
Leave the sum of forthvar({n1+n2}) .},{{-},{*},{/},{MOD}})
CODE_HEADER({+},{PLUS})
        POP     AX      ;{(S1) <- (S1) + (S2)}
        POP     BX
        ADD     AX,BX
        _PUSH
;
worddoc({DOUBLE},{D+},{d_plus},{d1 d2 --- dsum},{},{
Leave the double number sum of two double numbers.},{{DMINUS},{+}})
CODE_HEADER({D+},{DPLUS})
        POP     AX      ;{ YHW}
        POP     DX      ;{ YLW}
        POP     BX      ;{ XHW}
        POP     CX      ;{ XLW}
        ADD     DX,CX   ;{ SLW}
        ADC     AX,BX   ;{ SHW}
        _2PUSH
;
worddoc({OPERATOR},{MINUS},{minus},{n1 --- n2},{L0},{
Leave the two's complement of a number, i.e.
 forthvar({n2}) is forthvar({-n1})
},{{-}})                       
CODE_HEADER({MINUS},{MINUS})
        POP     AX
        NEG     AX
        _PUSH
;
worddoc({DOUBLE},{DMINUS},{d_minus},{d1 --- d2},{},{
Leave the double number two's complement of a double number, i.e.
 forthvar({d2}) is forthvar({-d1}) },{{D+}})
CODE_HEADER({DMINUS},{DMINU})
        POP     BX
        POP     CX
        SUB     AX,AX
        MOV     DX,AX
        SUB     DX,CX   ;{ MAKE 2'S COMPLEMENT}
        SBB     AX,BX   ;{ HIGH CELL}
        _2PUSH
        PAGE
;
CODE_HEADER({OVER},{OVER})
        POP     DX
        POP     AX
        PUSH    AX
        _2PUSH
;
CODE_HEADER({DROP},{DROP})
        POP     AX
        _NEXT
;
CODE_HEADER({SWAP},{SWAP})
        POP     DX
        POP     AX
        _2PUSH
;
CODE_HEADER({DUP},{LDUP})
        POP     AX
        PUSH    AX
        _PUSH
        PAGE
CODE_HEADER({2DUP},{TDUP})
        POP     AX
        POP     DX
        PUSH    DX
        PUSH    AX
        _2PUSH
;
CODE_HEADER({+!},{PSTOR})
        POP     BX      ;{ADDRESS}
        POP     AX      ;{INCREMENT}
        ADD     [BX],AX
        _NEXT
;
CODE_HEADER({TOGGLE},{TOGGL})
        POP     AX      ;{BIT PATTERN}
        POP     BX      ;{ADDR}
        XOR     [BX],AX ;
        _NEXT
;
CODE_HEADER({@},{FETCH})
        POP     BX
        MOV     AX,[BX]
        _PUSH
        PAGE
CODE_HEADER({C@},{CFET})
        POP     BX
        XOR     AX,AX
        MOV     AL,[BX]
        _PUSH
;
CODE_HEADER({2@},{TFET})
        POP     BX      ;{ADDR}
        MOV     AX,[BX] ;{MSW}
        MOV     DX,[BX+CELLS(1)]        ;{LSW}
        _2PUSH
;
worddoc({MEMORY},{!},{store},{n addr ---},{L0},{
Store 16 bits of n at address.},{})
CODE_HEADER({!},{STORE})
        POP     BX      ;{ADDR}
        POP     AX      ;{DATA}
        MOV     [BX],AX
        _NEXT
;
CODE_HEADER({C!},{CSTOR})
        POP     BX      ;{ADDR}
        POP     AX      ;{DATA}
        MOV     [BX],AL
        _NEXT
;
CODE_HEADER({2!},{TSTOR})
        POP     BX      ;{ADDR}
        POP     AX      ;{MSW}
        MOV     [BX],AX
        POP     AX      ;{LSW}
        MOV     [BX+CELLS(1)],AX
        _NEXT
;
CODE_HEADER({L@},{LFET})
_BITS32_1_({
        POP     BX      ;MEM LOC
        POP     AX      ;SEG REG VAL
        MOV     DX,10H
        MUL     DX
        ADD     AX,BX
        MOV     AX,[AX]
        _PUSH
});_END___1__({ _BITS32_1_})
_BITS16_1_({
        POP     BX      ;MEM LOC
        POP     CX      ;SEG REG VAL
        _SWITCH_1_({JMPHERE_FROM_PROT})
        MOV     DX,DS   ;{ Leave this for real mode code.}
        MOV     DS,CX
        MOV     BX,[BX]
        MOV     DS,DX
        _SWITCH_1_({JMPHERE_FROM_REAL})
        PUSH    BX
        _NEXT
});_END___1__({ _BITS16_1_})
;
CODE_HEADER({L!},{LSTORE})
_BITS32_1_({
        POP     BX      ;MEM LOC
        POP     AX      ;SEG REG VAL
        MOV     DX,10H
        MUL     DX
        ADD     AX,BX
        POP     BX
        MOV     [AX],BX
        _NEXT
});_END___1__({ _BITS32_1_})
_BITS16_1_({
        POP     BX
        POP     CX
        POP     DX
        _SWITCH_1_({JMPHERE_FROM_PROT})
        MOV     AX,DS
        MOV     DS,CX
        MOV     [BX],DX
        MOV     DS,AX
        _SWITCH_1_({JMPHERE_FROM_REAL})
        _NEXT
});_END___1__({ _BITS16_1_})
        PAGE
        ;{ At line     LINE ~1000}
;
worddoc({DEFINING},{:},{colon},{},{P,E,L0},{
Used in the form called a colon-definition:
forthexample({: cccc     ...     ;})
Creates a dictionary entry defining forthvar({cccc}) as equivalent to the
following sequence of Forth word definitions '...' until the next
forthcode({';'}) or forthcode({';CODE'}) .
The compiling process is done by the text interpreter as long as
forthcode({STATE}) is non-zero. Other details are that the forthcode({CONTEXT}) vocabulary is
set to the forthcode({CURRENT}) vocabulary and that words with the precedence bit
set (P) are executed rather than being compiled.},{} )
HEADER({:},{COLON},{DOCOL},1)
        DC      QEXEC
        DC      SCSP
        DC      CURR
        DC      FETCH
        DC      CONT
        DC      STORE
        DC      CREAT
        DC      RBRAC
        DC      PSCOD
DOCOL:  LEA     BP,[BP-CELLS(1)]
        MOV     [BP],SI ;{R1 <- (IP)}
        LEA     SI,[BX+CELLS(1)]  ;{(IP) <- (PFA)}
;        CALL    DISPLAYSI
_SWITCH_1_({
        _SWITCH_1_({JMPHERE_FROM_FORTH})
        ;{ Allow interrupts to happen.}
        _SWITCH_1_({JMPHERE_FROM_OS})
});_END___1__({ _SWITCH_1_})
        _NEXT
;
worddoc({DEFINING},{;},{semicolon},{},{P,C,L0},{
Terminate a colon-definition and stop further compilation. Compiles
the run-time forthcode({;S}) .},{})
HEADER({;},{SEMI},{DOCOL},1)
        DC      QCSP
        DC      COMP
        DC      SEMIS
        DC      SMUDG
        DC      LBRAC
        DC      SEMIS
        PAGE
HEADER({CONSTANT},{CON},{DOCOL})
        DC      CREAT
        DC      SMUDG
        DC      COMMA
        DC      PSCOD
DOCON:  MOV     AX,[BX+CELLS(1)] ;{GET DATA FROM PFA}
        _PUSH
;{               }
HEADER({VARIABLE},{VAR},{DOCOL})
        DC      CON
        DC      PSCOD
DOVAR:  LEA     AX,[BX+CELLS(1)] ;{(AX) <- PFA}
        _PUSH
;
HEADER({USER},{USER},{DOCOL})
        DC      CON
        DC      PSCOD
DOUSE:  MOV     BX,[BX+CELLS(1)] ;{PFA  }
        MOV     DI,_CELL_PTR[USINI+CELLS(1)]   ;{USER VAR ADDRESS}
        LEA     AX,[BX+DI]      ;{ADDR OF VARIABLE}
        _PUSH
        PAGE
CODE_HEADER({0},{ZERO})
        XOR     AX,AX
        _PUSH
;
CODE_HEADER({1},{ONE})
        MOV     AX,1
        _PUSH
;
CODE_HEADER({2},{TWO})   ;{ Needed also CELLS}
        MOV     AX,2
        _PUSH
;
CODE_HEADER({3},{THREE})
        MOV     AX,3
        _PUSH
;
HEADER({BL},{BLS},{DOCON})
;{      THIS IS ONLY A SPAC}
        DC      20H
;
HEADER({C/L},{CSLL},{DOCON})
        DC      64
;
_HIGH_BUF_1_({
HEADER({FIRST},{FIRST},{DOCON})
        DC      BUF1
});_END___1__({ _HIGH_BUF_1_})
;
_LOW_BUF_1_({
        _ALIGN  2       ; Otherwise INT 13 hangs when filling
        ; in into the lba block as a buffer address.
HEADER({FIRST},{FIRST},{DOVAR})
BUF1:    EQU     FIRST+CW     ; Must be even!
        _RESB    (KBBUF+2*CW)*NBUF
});_END___1__({ _LOW_BUF_1_})
;
;{ THE END OF THE BUFFERS}
HEADER({LIMIT},{LIMIT},{DOCON})
        DC      BUF1+(KBBUF+2*CW)*NBUF
;{ THE END  OF THE MEMORY }
HEADER({EM},{LEM},{DOCON})
        DC      EM
;
HEADER({B/BUF},{BBUF},{DOCON})
        DC      KBBUF
;
worddoc({SCREEN},{B/SCR},{bytes_per_screen},{--- n},{},{
This constant leaves the number of blocks per editing screen. 
An editing screen is 1024 bytes organized as 16 lines of
64 characters each.
In fact forthcode({B/SCR}) is 1, and so screens correspond one to one with
blocks. },{{BLOCK}})
HEADER({B/SCR},{BSCR},{DOCON})
        DC      400H/KBBUF
;
        PAGE
;
;{ All user variables are initialised }
;{ with the values from USINI.}
;{ The implementation relies on the initialisation of }
;{ those with numbers (1..11), so change in concord with USINI.}
HEADER({U0},{UZERO},{DOUSE})
        DC      CELLS(1)
;
HEADER({RUBOUT},{RUBOUT},{DOUSE})
        DC      CELLS(2)
;
HEADER({S0},{SZERO},{DOUSE})
        DC      CELLS(3)
;
HEADER({R0},{RZERO},{DOUSE})
        DC      CELLS(4)
;
HEADER({TIB},{TIB},{DOUSE})
        DC      CELLS(5)
;
HEADER({WIDTH},{WIDTHE},{DOUSE})
        DC      CELLS(6)
;
worddoc({ERROR},{WARNING},{warning},{--- addr},{U},{
A user variable containing a value controlling messages. If = 1 disc
is present, and screen 4 of drive 0 is the base location for
messages. If = 0, no disc is present and messages will be presented
by number. If = -1, execute forthcode({(ABORT)}) for a user specified proceedure.
},{{MESSAGE},{ERROR}})
HEADER({WARNING},{WARN},{DOUSE})
        DC      CELLS(7)
;
HEADER({FENCE},{FENCE},{DOUSE})
        DC      CELLS(8)
;
worddoc({DICTIONARY},{DP},{d_P},{---- addr},{U,L},{
A user variable, the dictionary pointer, which contains the address
of the next free memory above the dictionary. The value may be read
by forthcode({HERE}) and altered by forthcode({ALLOT}) .},{})
HEADER({DP},{LDP},{DOUSE})
        DC      CELLS(9)
;
HEADER({VOC-LINK},{VOCL},{DOUSE})
        DC      CELLS(10)
;
HEADER({OFFSET},{OFSET},{DOUSE})
        DC      CELLS(11)
;
;{ End of user variables with fixed place.}
;
worddoc({SCREEN},{SCR},{s_c_r},{--- addr},{U},{
A user variable containing the screen number most recently reference
by forthcode({LIST}) .},{{BLOCK}})
HEADER({SCR},{SCR},{DOUSE})
        DC      CELLS(14)
;
HEADER({CONTEXT},{CONT},{DOUSE})
        DC      CELLS(16)
;
HEADER({CURRENT},{CURR},{DOUSE})
        DC      CELLS(17)
;
HEADER({STATE},{STATE},{DOUSE})
        DC      CELLS(18)
;
HEADER({BASE},{BASE},{DOUSE})
        DC      CELLS(19)
;
HEADER({DPL},{DPL},{DOUSE})
        DC      CELLS(20)
;
HEADER({FLD},{LFLD},{DOUSE})
        DC      CELLS(21)
;
worddoc({SECURITY},{CSP},{c_s_p},{---- addr},{U},{
A user variable temporarily storing the stack pointer position, for
compilation error checking.},{})
HEADER({CSP},{LCSP},{DOUSE})
        DC      CELLS(22)
;
HEADER({R#},{RNUM},{DOUSE})
        DC      CELLS(23)
;
HEADER({HLD},{HLD},{DOUSE})
        DC      CELLS(24)
;
HEADER({IN},{LIN},{DOUSE})
        DC      CELLS(25)
;
HEADER({OUT},{LOUT},{DOUSE})
        DC      CELLS(26)
;
HEADER({BLK},{BLK},{DOUSE})
        DC      CELLS(27)
;
;{========== END USER VARIABLES =============;}
        PAGE
CODE_HEADER({1+},{ONEP})
        POP     AX
        INC     AX
        _PUSH
;
CODE_HEADER({2+},{TWOP})
        POP     AX
        ADD     AX,2
        _PUSH
;
;
CODE_HEADER({CELL+},{CELLP})
        POP     AX
        ADD     AX,CW
        _PUSH
;
worddoc({DICTIONARY},{HERE},{here},{--- addr},{L0},{
Leave the address of the next available dictionary location.},{{DP}} })
HEADER({HERE},{HERE},{DOCOL})
        DC      LDP
        DC      FETCH
        DC      SEMIS
;
worddoc({DICTIONARY},{ALLOT},{allot},{n ---},{L0},{
Add the signed number to the dictionary pointer forthcode({DP}) . May be used to
reserve dictionary space or re-origin memory. As the Pentium is a byte-addressable
machine forthvar({n}) counts bytes. },{{CELL+}})
HEADER({ALLOT},{ALLOT},{DOCOL})
        DC      LDP
        DC      PSTOR
        DC      SEMIS
;
HEADER({,},{COMMA},{DOCOL})
        DC      HERE
        DC      STORE
        DC      LCELL
        DC      ALLOT
        DC      SEMIS
;
HEADER({C,},{CCOMM},{DOCOL})
        DC      HERE
        DC      CSTOR
        DC      ONE
        DC      ALLOT
        DC      SEMIS
;
worddoc({OPERATOR},{-},{minus_sign},{n1 n2 --- diff},{L0},{
Leave the difference of forthvar({n1-n2}) .},{{MINUS},{+},{*},{/},{MOD}})
CODE_HEADER({-},{LSUB})
        POP     DX      ;{S1}
        POP     AX
        SUB     AX,DX
        _PUSH   ;{S1 = S2 - S1}
;
worddoc({LOGIC},{=},{equals},{n1 n2 --- f},{L0},{
Leave a true flag if forthvar({n1=n2}) ; otherwise leave a false flag.},{{<},{>},{0=},{-}})
HEADER({=},{EQUAL},{DOCOL})
        DC      LSUB
        DC      ZEQU
        DC      SEMIS
;
worddoc({LOGIC},{<},{less_than},{n1 n2 --- f},{L0},{
Leave a true flag if n1 is less than n2; otherwise leave a false
flag.},{{=},{>},{0<}})                     
CODE_HEADER({<},{LESS})
        POP     DX      ;{S1}
        POP     AX      ;{S2}
        MOV     BX,DX
        XOR     BX,AX   ;{TEST FOR EQUAL SIGNS}
        JS      LES1    ;{SIGNS ARE NOT THE SAME}
        SUB     AX,DX
LES1:   OR      AX,AX   ;{TEST SIGN BIT}
        MOV     AX,0    ;{ASSUME FALSE}
        JNS     LES2    ;{NOT LESS THAN}
        INC     AX      ;{TRUE (1)}
LES2:   _PUSH
;
HEADER({U<},{ULESS},{DOCOL})
        DC      TDUP
        DC      LXOR,ZLESS
        _0BRANCH(ULES1) ;{IF}
        DC      DROP,ZLESS
        DC      ZEQU
        _BRANCH(ULES2)
ULES1:  DC      LSUB,ZLESS      ;{ELSE}
ULES2:  DC      SEMIS           ;{ENDIF}
;
worddoc({LOGIC},{>},{greater_than},{n1 n2 --- f},{L0},{
Leave a true flag if forthvar({n1}) is greater than forthvar({n2}) ; otherwise a false flag.},{{>},{=},{0<}})
HEADER({>},{GREAT},{DOCOL})
        DC      SWAP
        DC      LESS
        DC      SEMIS
;
CODE_HEADER({ROT},{ROT})
        POP     DX      ;{S1}
        POP     BX      ;{S2}
        POP     AX      ;{S3}
        PUSH    BX
        _2PUSH
;
HEADER({SPACE},{SPACE},{DOCOL})
        DC      BLS
        DC      EMIT
        DC      SEMIS
;
HEADER({-DUP},{DDUP},{DOCOL})
        DC      LDUP
        _0BRANCH(DDUP1) ;{ IF}
        DC      LDUP    ;{ENDIF}
DDUP1:  DC      SEMIS
;
HEADER({TRAVERSE},{TRAV},{DOCOL})
        DC      SWAP
TRAV1:  DC      OVER    ;{BEGIN}
        DC      PLUS
        DC      LIT,7FH
        DC      OVER
        DC      CFET
        DC      LESS
        _0BRANCH(TRAV1) ;{UNTIL}
        DC      SWAP
        DC      DROP
        DC      SEMIS
;
HEADER({LATEST},{LATES},{DOCOL})
        DC      CURR
        DC      FETCH
        DC      FETCH
        DC      SEMIS
;
worddoc({DICTIONARY},{LFA},{link_field_address},{pfa --- lfa},{},{
Convert the parameter field address of a dictionary definition to
its link field address.},{{'},{PFA}})
HEADER({LFA},{LFA},{DOCOL})
        DC      LIT,CELLS(2)
        DC      LSUB
        DC      SEMIS
;
worddoc({DICTIONARY},{CFA},{cfa},{pfa --- cfa},{},{
Convert the parameter field address of a definition to its code
field address.},{{'},{PFA}})
HEADER({CFA},{CFA},{DOCOL})
        DC      LCELL
        DC      LSUB
        DC      SEMIS
;
worddoc({DICTIONARY},{NFA},{name_field_address},{pfa --- nfa},{},{
Convert the parameter field address of a definition to its name
field.},{{'},{PFA}})
HEADER({NFA},{NFA},{DOCOL})
        DC      LIT,1+CELLS(2)
        DC      LSUB
        DC      LIT,-1
        DC      TRAV
        DC      SEMIS
;
worddoc({DICTIONARY},{PFA},{parameter_field_address},{nfa --- pfa},{},{
Convert the name field address of a compiled definition to its
parameter field address.},{{'},{CFA},{LFA},{NFA}})
HEADER({PFA},{PFA},{DOCOL})
        DC      ONE
        DC      TRAV
        DC      LIT,1+CELLS(2)
        DC      PLUS
        DC      SEMIS
        PAGE
        ;{ At line     LINE ~1500}

worddoc({SECURITY},{!CSP},{store_c_s_p},{},{},{
Save the stack position in forthcode({CSP}). Used as part of the compiler
security.},{})
HEADER({!CSP},{SCSP},{DOCOL})
        DC      SPFET
        DC      LCSP
        DC      STORE
        DC      SEMIS
;
worddoc({ERROR},{?ERROR},{question_error},{f n --},{},{
  Issue an error message number forthcode({n}) , if the boolean flag is true.},{ERROR})
HEADER({?ERROR},{QERR},{DOCOL})
        DC      SWAP
        _0BRANCH(QERR1) ;{IF}
        DC      ERROR
        _BRANCH(QERR2)  ;{ELSE}
QERR1:  DC      DROP    ;{ENDIF}
QERR2:  DC      SEMIS
;
HEADER({?COMP},{QCOMP},{DOCOL})
        DC      STATE
        DC      FETCH
        DC      ZEQU
        DC      LIT,11H
        DC      QERR
        DC      SEMIS
        PAGE
HEADER({?EXEC},{QEXEC},{DOCOL})
        DC      STATE
        DC      FETCH
        DC      LIT,12H
        DC      QERR
        DC      SEMIS
;
HEADER({?PAIRS},{QPAIR},{DOCOL})
        DC      LSUB
        DC      LIT,13H
        DC      QERR
        DC      SEMIS
;
worddoc({SECURITY},{?CSP},{question_c_s_p},{},{},{
Issue error message if stack position differs from value saved in
forthvar({CSP}) .},{})
HEADER({?CSP},{QCSP},{DOCOL})
        DC      SPFET
        DC      LCSP
        DC      FETCH
        DC      LSUB
        DC      LIT,14H
        DC      QERR
        DC      SEMIS
;
HEADER({?LOADING},{QLOAD},{DOCOL})
        DC      BLK
        DC      FETCH
        DC      ZEQU
        DC      LIT,16H
        DC      QERR
        DC      SEMIS
        PAGE
worddoc({COMPILING},{COMPILE},{compile},{},{C2},{
When the word containing forthcode({COMPILE}) executes, the execution address of
the word following forthcode({COMPILE}) is copied (compiled) into the dictionary.
This allows specific compilation situations to be handled in
addition to simply compiling an execution address (which the
interpreter already does).},{[COMPILE]})
HEADER({COMPILE},{COMP},{DOCOL})
        DC      QCOMP
        DC      FROMR
        DC      LDUP
        DC      CELLP
        DC      TOR
        DC      FETCH
        DC      COMMA
        DC      SEMIS
;
HEADER({[},{LBRAC},{DOCOL},1)
        DC      ZERO
        DC      STATE
        DC      STORE
        DC      SEMIS
;
HEADER({]},{RBRAC},{DOCOL})
        DC      LIT,0C0H
        DC      STATE
        DC      STORE
        DC      SEMIS
        PAGE
HEADER({SMUDGE},{SMUDG},{DOCOL})
        DC      LATES
        DC      LIT,20H
        DC      TOGGL
        DC      SEMIS
;
HEADER({HEX},{HEX},{DOCOL})
        DC      LIT,16
        DC      BASE
        DC      STORE
        DC      SEMIS
;
HEADER({DECIMAL},{DECA},{DOCOL})
        DC      LIT,10
        DC      BASE
        DC      STORE
        DC      SEMIS
;
worddocsafe({DEFINING},{(;CODE)},{paren_semicolon_code},{},{C},{
The run-time proceedure, compiled by forthcode({;CODE}), that rewrites the code
field of the most recently defined word to point to the following
machine code sequence.},{{;CODE}})
HEADER({(;CODE)},{PSCOD},{DOCOL})
        DC      FROMR
        DC      LATES
        DC      PFA
        DC      CFA
        DC      STORE
        DC      SEMIS
;
worddoc({DEFINING},{;CODE},{semicolon_code},{},{P,C,L0},{
Used in the form:
  forthsamp({: cccc .... ;CODE})
assembly mnemonics Stop compilation and terminate a new defining
word forthvar({cccc}) by compiling forthcode({(;CODE)}). Set the forthcode({CONTEXT}) vocabulary to
forthcode({ASSEMBLER}) , assembling to machine code the following mnemonics.

When forthvar({cccc}) later executes in the form:
                     forthsamp({cccc nnnn})
the word forthvar({nnnn}) will be created with its execution proceedure given by
the machine code following forthvar({cccc}) .
That is, when forthvar({nnnn}) is executed, it does so by jumping to the code
after forthvar({nnnn}) . An existing defining word must exist in forthvar({cc}) prior to
forthcode({;CODE}) .},{(;CODE)})
HEADER({;CODE},{SEMIC},{DOCOL},1)
        DC      QCSP
        DC      COMP
        DC      PSCOD
        DC      LBRAC
SEMI1:  DC      NOOP    ;{ ( ASSEMBLER )}
        DC      SEMIS
;
worddoc({DEFINING},{BUILDS},{builds},{},{C,L0},{
This word is called "smaller_sign_BUILDS" but the html chokes over that name.
Used within a colon-definition:
         forthexample({: cccc  BUILDS  ... DOES>     ...    ;})
Each time forthvar({cccc}) is executed, forthcode({BUILDS}) defines a new word with a
high-level execution proceedure. Executing forthvar({cccc}) in the form:
            forthsamp({cccc nnnn}) 
uses forthcode({BUILDS}) to create a dictionary entry for forthvar({nnnn}) with a call to
the forthcode({DOES>}) part for forthvar({nnnn}) . When forthvar({nnnn}) is later executed, it has the
address of its parameter area on the stack and executes the words
after forthcode({DOES>}) in forthvar({cccc}) . forthcode({BUILDS}) and forthcode({DOES>}) allow runtime proceedures to
written in high-level rather than in assembler code (as required by
forthcode({;CODE}) ). },{})
HEADER({<BUILDS},{BUILD},{DOCOL})
        DC      ZERO
        DC      CON
        DC      SEMIS
;
HEADER({DOES>},{DOES},{DOCOL})
        DC      FROMR
        DC      LATES
        DC      PFA
        DC      STORE
        DC      PSCOD
DODOE:  LEA     BP,[BP-CELLS(1)]
        MOV     [BP],SI ;{R1 <- (IP)}
        MOV     SI,[BX+CELLS(1)] ;{NEW IP }
        LEA     AX,[BX+2*CELLS(1)]
        _PUSH
;
HEADER({COUNT},{COUNT},{DOCOL})
        DC      LDUP
        DC      ONEP
        DC      SWAP
        DC      CFET
        DC      SEMIS
;
HEADER({-TRAILING},{DTRAI},{DOCOL})
        DC      LDUP
        DC      ZERO
        DC      XDO     ;{DO}
DTRA1:  DC      OVER
        DC      OVER
        DC      PLUS
        DC      ONE
        DC      LSUB
        DC      CFET
        DC      BLS
        DC      LSUB
        _0BRANCH(DTRA2) ;{IF}
        DC      LLEAV
        _BRANCH(DTRA3)  ;{ ELSE}
DTRA2:  DC      ONE
        DC      LSUB    ;{ ENDIF}
DTRA3:  _LOOP(DTRA1)    ;{ LOOP}
        DC      SEMIS
        PAGE
        ;{ At line     LINE ~2000}
worddocsafe({PARSING},{(.")},{paren_dot_quote},{},{C+},{
The run-time proceedure, compiled by forthcode({."}) which transmits the
following in-line text to the selected output device.},{{."}}) 
HEADER_SGQ({(.")},{PDOTQ},{DOCOL})
        DC      RR
        DC      COUNT
        DC      LDUP
        DC      ONEP
        DC      FROMR
        DC      PLUS
        DC      TOR
        DC      LTYPE
        DC      SEMIS
;
HEADER_SGQ({."},{DOTQ},{DOCOL},1)
        DC      LIT,22H
        DC      STATE
        DC      FETCH
        _0BRANCH(DOTQ1) ;{ IF}
        DC      COMP
        DC      PDOTQ
        DC      LWORD
        DC      HERE
        DC      CFET
        DC      ONEP
        DC      ALLOT
        _BRANCH(DOTQ2)  ;{ ELSE}
DOTQ1:  DC      LWORD
        DC      HERE
        DC      COUNT
        DC      LTYPE   ;{ ENDIF}
DOTQ2:  DC      SEMIS
        PAGE
HEADER({QUERY},{QUERY},{DOCOL})
        DC      TIB
        DC      FETCH
        DC      LIT,RTS/2
        DC      EXPEC
        DC      ZERO
        DC      LIN
        DC      STORE
        DC      SEMIS
        PAGE
HEADER_NULL({(NULL)},{NULL},{DOCOL})
;{       Special header putting an ASCII NULL in the dictionary.}
        DC      BLK
        DC      FETCH
        _0BRANCH(NULL1) ;{ IF}
        DC      ONE
        DC      BLK
        DC      PSTOR
        DC      ZERO
        DC      LIN
        DC      STORE
        DC      BLK
        DC      FETCH
        DC      BSCR
        DC      ONE
        DC      LSUB
        DC      LAND
        DC      ZEQU
        _0BRANCH(NULL2) ;{ IF}
        DC      QEXEC
        DC      FROMR
        DC      DROP    ;{ ENDIF}
NULL2:  _BRANCH(NULL3)  ;{ ELSE}
NULL1:  DC      FROMR
        DC      DROP    ;{ ENDIF}
NULL3:  DC      SEMIS
;
CODE_HEADER({FILL},{FILL})
        POP     AX      ;{ FILL CHAR}
        POP     CX      ;{ FILL COUNT}
        POP     DI      ;{ BEGIN ADDR}
;{       MOV    BX,DS}
;{       MOV    ES,BX   ; ES <- DS}
        CLD             ;{ INC DIRECTION}
        REP     STOSB   ;{STORE BYTE}
        _NEXT
;
HEADER({ERASE},{LERASE},{DOCOL})
        DC      ZERO
        DC      FILL
        DC      SEMIS
;
HEADER({BLANKS},{BLANK},{DOCOL})
        DC      BLS
        DC      FILL
        DC      SEMIS
;
HEADER({HOLD},{HOLD},{DOCOL})
        DC      LIT,-1
        DC      HLD
        DC      PSTOR
        DC      HLD
        DC      FETCH
        DC      CSTOR
        DC      SEMIS
;
HEADER({PAD},{PAD},{DOCOL})
        DC      HERE
        DC      LIT,84
        DC      PLUS
        DC      SEMIS
;
HEADER({WORD},{LWORD},{DOCOL})
        DC      BLK
        DC      FETCH
        _0BRANCH(WORD1) ;{ IF}
        DC      BLK
        DC      FETCH
        DC      BLOCK
        _BRANCH(WORD2)  ;{ ELSE}
WORD1:  DC      TIB
        DC      FETCH      ;{ ENDIF}
WORD2:  DC      LIN
        DC      FETCH
        DC      PLUS
        DC      SWAP
        DC      ENCL
        DC      HERE
        DC      LIT,22H
        DC      BLANK
        DC      LIN
        DC      PSTOR
        DC      OVER
        DC      LSUB
        DC      TOR
        DC      RR
        DC      HERE
        DC      CSTOR
        DC      PLUS
        DC      HERE
        DC      ONEP
        DC      FROMR
        DC      LCMOVE
        DC      SEMIS
;
worddocsafe({NUMBER},{(NUMBER)},{paren_number},{d1 addr1 --- d2 addr2},{},{
Convert the ascii text beginning at forthvar({addr1+l}) with regard to forthcode({BASE}) . The
new value is accumulated into double number forthvar({d1}) , being left as double number 
forthvar({d2}) .
 forthvar({addr2}) is the address of the first unconvertable digit.},{{NUMBER}})
HEADER({(NUMBER)},{PNUMB},{DOCOL})
PNUM1:  DC      ONEP    ;{ BEGIN}
        DC      LDUP
        DC      TOR
        DC      CFET
        DC      BASE
        DC      FETCH
        DC      DIGIT
        _0BRANCH(PNUM2) ;{ WHILE}
        DC      SWAP
        DC      BASE
        DC      FETCH
        DC      USTAR
        DC      DROP
        DC      ROT
        DC      BASE
        DC      FETCH
        DC      USTAR
        DC      DPLUS
        DC      DPL
        DC      FETCH
        DC      ONEP
        _0BRANCH(PNUM3) ;{ IF}
        DC      ONE
        DC      DPL
        DC      PSTOR   ;{ ENDIF}
PNUM3:  DC      FROMR
        _BRANCH(PNUM1)  ;{ REPEAT}
PNUM2:  DC      FROMR
        DC      SEMIS
;
worddoc({NUMBER},{NUMBER},{number},{addr --- d},{},{
Convert a character string left at forthvar({addr}) with a preceeding count, to
a signed double number forthvar({d}) , using the current numeric base. If a
decimal point is encountered in the text, its position will be
given in forthcode({DPL}) , but no other effect occurs. If numeric conversion is
not possible, an error message will be given.},{{BASE}})
HEADER({NUMBER},{NUMB},{DOCOL})
        DC      ZERO
        DC      ZERO
        DC      ROT
        DC      LDUP
        DC      ONEP
        DC      CFET
        DC      LIT,2DH
        DC      EQUAL
        DC      LDUP
        DC      TOR
        DC      PLUS
        DC      LIT,-1
NUMB1:  DC      DPL     ;{ BEGIN}
        DC      STORE
        DC      PNUMB
        DC      LDUP
        DC      CFET
        DC      BLS
        DC      LSUB
        _0BRANCH(NUMB2) ;{ WHILE}
        DC      LDUP
        DC      CFET
        DC      LIT,2EH
        DC      LSUB
        DC      ZERO
        DC      QERR
        DC      ZERO
        _BRANCH(NUMB1)  ;{ REPEAT}
NUMB2:  DC      DROP
        DC      FROMR
        _0BRANCH(NUMB3) ;{ IF}
        DC      DMINU   ;{ ENDIF}
NUMB3:  DC      SEMIS
;
worddoc({DICTIONARY},{-FIND},{minus_find},{--- pfa b tf (found) --- ff (not found)},{},{
Accepts the next text word (delimited by blanks) in the input stream
to forthcode({HERE}) , and searches the forthcode({CONTEXT}) and then forthcode({CURRENT}) vocabularies
for a matching entry. If found, the dictionary entry's parameter
field address, its length byte, and a boolean true is left.
Otherwise, only a boolean false is left.},{{(FIND)}})
HEADER({-FIND},{DFIND},{DOCOL})
        DC      BLS
        DC      LWORD
        DC      HERE
        DC      CONT
        DC      FETCH
        DC      FETCH
        DC      PFIND
        DC      LDUP
        DC      ZEQU
        _0BRANCH(DFIN1) ;{IF}
        DC      DROP
        DC      HERE
        DC      LATES
        DC      PFIND   ;{ENDIF}
DFIN1:  DC      SEMIS
;
worddocsafe({ERROR},{(ABORT)},{paren_abort},{},{},{
Executes after an error when forthcode({WARNING}) is -1. This word normally
executes forthcode({ABORT}), but may be altered (with care) to a user's
alternative proceedure forthvar({AP}) as follows: 
forthsamp({ ' forthvar({AP}) CFA   ' (ABORT) !})
},{})
HEADER({(ABORT)},{PABOR},{DOCOL})
        DC      ABORT
        DC      SEMIS
;
worddoc({ERROR},{ERROR},{error},{line --- in blk},{},{
Execute error notification and restart of system. forthcode({WARNING}) is first
examined. If 1, the text of line forthvar({n}) , relative to screen 4 of drive O
is printed. This line number may be positive or negative, and beyond
just screen 4. If forthcode({WARNING}) is zero, forthvar({n}) is just printed as a message number
(non disc installation). If forthcode({WARNING}) is -l, the definition forthcode({(ABORT)}) is
executed, which executes the system forthcode({ABORT}) . The user may cautiously
modify this execution by altering forthcode({(ABORT)}) .
fig-FORTH saves the contents of forthcode({IN}) and forthcode({BLK}) to assist in determining
the location of the error. Final action is execution of forthcode({QUIT}) .},{QERROR})
HEADER({ERROR},{ERROR},{DOCOL})
        DC      WARN
        DC      FETCH
        DC      ZLESS
        _0BRANCH(ERRO1) ;{IF}
        DC      PABOR   ;{ENDIF}
ERRO1:  DC      HERE
        DC      COUNT
        DC      LTYPE
        DC      PDOTQ
        _STRING({? })
        DC      MESS
        DC      SPSTO
;
;{      CHANGE FROM FIG MODEL}
;{      DC LIN,FETCH,BLK,FETCH}
;
        DC      BLK,FETCH
        DC      DDUP
        _0BRANCH(ERRO2) ;{ IF}
        DC      LIN,FETCH
        DC      SWAP    ;{ENDIF}
ERRO2:  DC      QUIT
;
HEADER({ID.},{IDDOT},{DOCOL})
        DC      PAD
        DC      LIT,20H
        DC      LIT,5FH
        DC      FILL
        DC      LDUP
        DC      PFA
        DC      LFA
        DC      OVER
        DC      LSUB
        DC      PAD
        DC      SWAP
        DC      LCMOVE
        DC      PAD
        DC      COUNT
        DC      LIT,1FH
        DC      LAND
        DC      ONE
        DC      LSUB
        DC      TDUP
        DC      LTYPE
        DC      PLUS
        DC      CFET
        DC      LIT,07FH
        DC      LAND
        DC      EMIT
        DC      SPACE
        DC      SEMIS
        PAGE
HEADER({CREATE},{CREAT},{DOCOL})
        DC      DFIND
        _0BRANCH(CREA1) ;{IF}
        DC      DROP
        DC      NFA
        DC      IDDOT
        DC      LIT,4
        DC      MESS
        DC      SPACE   ;{ENDIF}
CREA1:  DC      HERE
        DC      LDUP
        DC      CFET
        DC      WIDTHE
        DC      FETCH
        DC      MIN
        DC      ONEP
        DC      ALLOT
        DC      LDUP
        DC      LIT,0A0H
        DC      TOGGL
        DC      HERE
        DC      ONE
        DC      LSUB
        DC      LIT,80H
        DC      TOGGL
        DC      LATES
        DC      COMMA
        DC      CURR
        DC      FETCH
        DC      STORE
        DC      HERE
        DC      CELLP
        DC      COMMA
        DC      SEMIS
        PAGE
worddoc({COMPILING},{[COMPILE]},{bracket_compile},{},{p,C},{
Used in a colon-definition in form:
forthexample({:  xxx    [COMPILE]   FORTH   ;})
forthcode({[COMPILE]}) will force the compilation of an immediate definitions,
that would otherwise execute during compilation. The above example
will select the forthcode({FORTH}) vocabulary then forthvar({xxx}) executes, rather than at
compile time.},{COMPILE})
HEADER({[COMPILE]},{BCOMP},{DOCOL},1)
        DC      DFIND
        DC      ZEQU
        DC      ZERO
        DC      QERR
        DC      DROP
        DC      CFA
        DC      COMMA
        DC      SEMIS
;
HEADER({LITERAL},{LITER},{DOCOL},1)
        DC      STATE
        DC      FETCH
        _0BRANCH(LITE1) ;{IF}
        DC      COMP
        DC      LIT
        DC      COMMA   ;{ENDIF}
LITE1:  DC      SEMIS
        PAGE
;
HEADER({DLITERAL},{DLITE},{DOCOL},1)
        DC      STATE
        DC      FETCH
        _0BRANCH(DLIT1) ;{ IF}
        DC      SWAP
        DC      LITER
        DC      LITER   ;{ ENDIF}
DLIT1:  DC      SEMIS
;
HEADER({?STACK},{QSTAC},{DOCOL})
        DC      SPFET
        DC      SZERO
        DC      FETCH
        DC      SWAP
        DC      ULESS
        DC      ONE
        DC      QERR
        DC      SPFET
        DC      HERE
        DC      LIT,80H
        DC      PLUS
        DC      ULESS
        DC      LIT,7
        DC      QERR
        DC      SEMIS
        PAGE
        ;{ At line     LINE ~2500}
HEADER({INTERPRET},{INTER},{DOCOL})
INTE1:  DC      DFIND   ;{BEGIN}
        _0BRANCH(INTE2) ;{IF}
        DC      STATE
        DC      FETCH
        DC      LESS
        _0BRANCH(INTE3) ;{IF}
        DC      CFA
        DC      COMMA
        _BRANCH(INTE4)  ;{ELSE}
INTE3:  DC      CFA
        DC      EXEC    ;{ENDIF}
INTE4:  DC      QSTAC
        _BRANCH(INTE5)  ;{ELSE}
INTE2:  DC      HERE
        DC      NUMB
        DC      DPL
        DC      FETCH
        DC      ONEP
        _0BRANCH(INTE6) ;{IF}
        DC      DLITE
        _BRANCH(INTE7)  ;{ELSE}
INTE6:  DC      DROP
        DC      LITER   ;{ENDIF}
INTE7:  DC      QSTAC   ;{ENDIF}
INTE5:  _BRANCH(INTE1)  ;{AGAIN}
        PAGE
HEADER({IMMEDIATE},{IMMED},{DOCOL})
        DC      LATES
        DC      LIT,40H
        DC      TOGGL
        DC      SEMIS
;
HEADER({VOCABULARY},{VOCAB},{DOCOL})
        DC      BUILD
        DC      LIT, 80H+1, CCOMM    ; Dummy name field
        DC      LIT, " "+80H, CCOMM
        DC      CURR
        DC      FETCH
        DC      LIT, 2, LSUB ; Skip backwards over dummy name field.
        DC      COMMA
        DC      HERE
        DC      VOCL
        DC      FETCH
        DC      COMMA
        DC      VOCL
        DC      STORE
        DC      DOES
DOVOC:  DC      TWOP    ; i.e. Skip ' ' name.
        DC      CONT
        DC      STORE
        DC      SEMIS
        PAGE
;
;{   THE LINKFIELD OF TASK IS A COLD START VALUE ONLY.}
;{   IT IS CHANGED EACH TIME A DEFINITION IS}
;{   APPENDED TO THE 'FORTH' VOCABULARY.}
;
HEADER({FORTH},{FORTH},{DODOE},1)
        DC      DOVOC
        DB      80H+1, " "+80H  ; Dummy name field
        DC      _NAME_FIELD(TASK)
        DC      0       ;{ END OF VOCABULARY LIST}
;
HEADER({DEFINITIONS},{DEFIN},{DOCOL})
        DC      CONT
        DC      FETCH
        DC      CURR
        DC      STORE
        DC      SEMIS
;
worddocsafe({PARSING},{(},{paren},{},{P,L0},{
Used in the form:
forthsamp({( cccc)})
Ignore a comment that will be delimited by a right
parenthesis on the same line. May occur during execution or in a
colon-definition. A blank after the leading parenthesis is required.},{})
HEADER({(},{PAREN},{DOCOL},1)
        DC      LIT,')'
        DC      LWORD
        DC      SEMIS
        PAGE
HEADER({QUIT},{QUIT},{DOCOL})
        DC      ZERO
        DC      BLK
        DC      STORE
        DC      LBRAC
QUIT1:  DC      RPSTO   ;{BEGIN}
        DC      CR
        DC      QUERY
        DC      INTER
        DC      STATE
        DC      FETCH
        DC      ZEQU
        _0BRANCH(QUIT2) ;{IF}
        DC      PDOTQ
        _STRING({OK})   ;{ENDIF}
QUIT2:  _BRANCH(QUIT1)  ;{AGAIN}
;
worddoc({ERROR},{ABORT},{abort},{},{L0},{
Clear the stacks and enter the execution state. Return control to
the operators terminal, printing a message appropriate to the
installation.},{})
HEADER({ABORT},{ABORT},{DOCOL})
        DC      SPSTO
        DC      DECA
        DC      QSTAC   ;{ IT DID TO & INCL THIS}
        DC      CR
        DC      DOTCPU
        DC      PDOTQ

_COMMENT
;{       If this is there it is an official release}
        DB      22
        DB      'IBM-PC Fig-Forth'
        DB      FIGREL+30H,ADOT,FIGREV+30H,ADOT,USRVER+30H
_ENDCOMMENT
;{       If this is there it is a beta release}
        _STRING({IBM-PC $RCSfile$ $Revision$ })
        DC      FORTH
        DC      DEFIN
        DC      QUIT
        PAGE
;{      WARM START VECTOR COMES HERE}
;
WRM:    MOV     SI,_OFFSET WRM1
        _NEXT
;
WRM1:   DC      WARM
;
HEADER({WARM},{WARM},{DOCOL})
        DC      MTBUF
        DC      ABORT
;
;{      COLD START VECTOR COMES HERE}
;
LCLD:    MOV     SI,_OFFSET CLD1  ;{ (IP) <-}
_COMMENT
_HOSTED_MSDOS_1_({
        PUSH    DS
        MOV     AX,0
        MOV     DS,AX           ;{TO VECTOR AREA}
        MOV     BX,08CH
        LEA     AX,[WRM]
        MOV     [BX],AX         ;{JUMP TO WRM ON <CTRL-BREAK>}
        INC     BX
        INC     BX
        MOV     [BX],CS
        POP     DS
});_END___1__({ _HOSTED_MSDOS_1_})
_ENDCOMMENT
        CLD                     ;{ DIR = INC}
        MOV     STACKPOINTER,_CELL_PTR[USINI+CELLS(3)]    ;{PARAM. STACK}
        MOV     BP,_CELL_PTR[USINI+CELLS(4)]    ;{RETURN STACK}
        _NEXT
;
CLD1:   DC      COLD
;
HEADER({COLD},{COLD},{DOCOL})
        DC      MTBUF
        DC      FIRST
        DC      USE,STORE
        DC      FIRST
        DC      PREV,STORE
        DC      LIT, USINI
        DC      LIT,USINI+CELLS(1)
        DC      FETCH
        DC      LIT,US
        DC      LCMOVE
        DC      LIT,USINI + CELLS(0)    ; I.e. lfa of TASK
        DC      FETCH
        DC      LIT,_VOC_LATEST(FORTH)
        DC      STORE
_RWFILE_1_({
        DC      BLINI
});_END___1__({_RWFILE_1_})
_LINUX_C_1_({
        DC      BLINI
});_END___1__({_LINUX_C_1_})
_LINUX_N_1_({
        DC      ZERO, LIT, TCGETS, TERMIO
        DC      LIT, ioctl, LINOS, ZLESS
        _0BRANCH(COLD1)
        DC      LIT, STTERM_A
        DC      LIT, _PARAMETER_FIELD(STTERM)
        DC      LIT, STTERM_B-STTERM_A
        DC      LCMOVE
        DC      LIT, EXPEC_A
        DC      LIT, _PARAMETER_FIELD(EXPEC)
        DC      LIT, EXPEC_B-EXPEC_A
        DC      LCMOVE
COLD1:
        DC      BLINI
});_END___1__({_LINUX_N_1_})
        DC      ABORT
;
        PAGE
worddoc({DOUBLE},{S->D},{s_to_d},{n --- d},{},{
Sign extend a single number to form a double number.},{})
CODE_HEADER({S->D},{STOD})
        POP     DX      ;{S1}
        SUB     AX,AX
        OR      DX,DX
        JNS     STOD1   ;{POS}
        DEC     AX      ;{NEG}
STOD1:  _2PUSH
;
HEADER({+-},{PM},{DOCOL})
        DC      ZLESS
        _0BRANCH(PM1)   ;{IF}
        DC      MINUS   ;{ENDIF}
PM1:    DC      SEMIS
;
HEADER({D+-},{DPM},{DOCOL})
        DC      ZLESS
        _0BRANCH(DPM1)  ;{IF}
        DC      DMINU   ;{ENDIF}
DPM1:   DC      SEMIS
;
worddoc({OPERATOR},{ABS},{abs},{n --- u},{L0},{
Leave the absolute value of forthvar({n}) as forthvar({u}) .},{})
HEADER({ABS},{LABS},{DOCOL})
        DC      LDUP
        DC      PM
        DC      SEMIS
;
worddoc({DOUBLE},{DABS},{d_abs},{d --- ud},{},{
Leave the absolute value forthvar({ud}) of a double number.},{{DMINUS}})
HEADER({DABS},{DABS},{DOCOL})
        DC      LDUP
        DC      DPM
        DC      SEMIS
;
worddoc({OPERATOR},{MIN},{min},{n1 n2 --- min},{L0},{
Leave the smaller of two numbers.},{{MAX}})
HEADER({MIN},{MIN},{DOCOL})
        DC      TDUP
        DC      GREAT
        _0BRANCH(MIN1)  ;{IF}
        DC      SWAP    ;{ENDIF}
MIN1:   DC      DROP
        DC      SEMIS
;
worddoc({OPERATOR},{MAX},{max},{n1 n2 --- max},{L0},{
Leave the greater of two numbers.},{{MIN}})
HEADER({MAX},{MAX},{DOCOL})
        DC      TDUP
        DC      LESS
        _0BRANCH(MAX1)  ;{IF}
        DC      SWAP    ;{ENDIF}
MAX1:   DC      DROP
        DC      SEMIS
;
worddoc({MULTIPLYING},{M*},{m_star},{n1 n2 --- d},{},{
A mixed magnitude math operation which leaves the double number
 forthvar({d}) : the signed product of two signed number forthvar({n1}) and forthvar({n2}) .},{{M/MOD},{M/},{*}})
HEADER({M*},{MSTAR},{DOCOL})
        DC      TDUP
        DC      LXOR
        DC      TOR
        DC      LABS
        DC      SWAP
        DC      LABS
        DC      USTAR
        DC      FROMR
        DC      DPM
        DC      SEMIS
;
worddoc({MULTIPLYING},{M/},{m_slash},{d n1 --- n2 n3},{},{
A mixed magnitude math operator which leaves the signed remainder forthvar({n2}) 
and signed quotient forthvar({n3}) from a double number dividend forthvar({d}) and divisor forthvar({n1}) .
The remainder takes its sign from the dividend.},{{M/MOD},{/},{M*}})
HEADER({M/},{MSLAS},{DOCOL})
        DC      OVER
        DC      TOR
        DC      TOR
        DC      DABS
        DC      RR
        DC      LABS
        DC      USLAS
        DC      FROMR
        DC      RR
        DC      LXOR
        DC      PM
        DC      SWAP
        DC      FROMR
        DC      PM
        DC      SWAP
        DC      SEMIS
;
worddocsafe({OPERATOR},{*},{star},{n1 n2 --- prod},{L0},{
Leave the signed product of two signed numbers.},{{+},{-},{/},{MOD}})
HEADER({*},{STAR},{DOCOL})
        DC      MSTAR
        DC      DROP
        DC      SEMIS
;
worddoc({MULTIPLYING},{/MOD},{slash_mod},{n1 n2 --- rem quot},{L0},{
Leave the remainder and signed quotient of forthvar({n1/n2}) . The remainder has
the sign of the dividend.},{{*/MOD},{*/},{M/MOD}}) 
HEADER({/MOD},{SLMOD},{DOCOL})
        DC      TOR
        DC      STOD
        DC      FROMR
        DC      MSLAS
        DC      SEMIS
;
worddoc({OPERATOR},{/},{slash},{n1 n2 --- quot},{L0},{
Leave the signed quotient of forthvar({n1/n2}) .
It the result is interpreted as logical value, it means forthvar({n1}) si not equal
to forthvar({n2}) },{{+},{-},{*},{MOD},{*/MOD}})
HEADER({/},{SLASH},{DOCOL})
        DC      SLMOD
        DC      SWAP
        DC      DROP
        DC      SEMIS
;
worddoc({OPERATOR},{MOD},{mod},{n1 n2 --- mod},{L0},{
Leave the remainder of forthvar({n1/n2}) , with the same sign as forthvar({n1}) .},
{{+},{-},{*},{/},MOD,{*/MOD}})
HEADER({MOD},{LMOD},{DOCOL})
        DC      SLMOD
        DC      DROP
        DC      SEMIS
;
worddoc({MULTIPLYING},{*/MOD},{start_slash_mod},{n1 n2 n3 --- n4 n5},{L0},{
Leave the quotient forthvar({n5}) and remainder forthvar({n4}) of the operation forthvar({n1*n2/n3}) A
31 bit intermediate product is used as for forthcode({*/}) .},{{*/},{/MOD}})
HEADER({*/MOD},{SSMOD},{DOCOL})
        DC      TOR
        DC      MSTAR
        DC      FROMR
        DC      MSLAS
        DC      SEMIS
;
worddoc({MULTIPLYING},{*/},{star_slash},{n1 n2 n3 --- n4},{L0},{
Leave the ratio forthvar({n4 = n1*n2/n3}) where all are signed numbers.
Retention of an intermediate 31 bit product permits greater accuracy
than would. be available with the sequence:
      forthsamp({n1 n2 * n3 /})
},{{*/MOD},{/MOD}})                          
HEADER({*/},{SSLA},{DOCOL})
        DC      SSMOD
        DC      SWAP
        DC      DROP
        DC      SEMIS
;
worddoc({MULTIPLYING},{M/MOD},{m_slash_mod},{ud1 u2 --- u3 ud4},{},{
An unsigned mixed magnitude math operation which leaves a double
quotient forthvar({ud4}) and remainder forthvar({u3}) , from a double dividend forthvar({ud1}) and single
divisor forthvar({u2}) .},{{/MOD},{M/},{M*}})  
HEADER({M/MOD},{MSMOD},{DOCOL})
        DC      TOR
        DC      ZERO
        DC      RR
        DC      USLAS
        DC      FROMR
        DC      SWAP
        DC      TOR
        DC      USLAS
        DC      FROMR
        DC      SEMIS
;
worddocsafe({SCREEN},{(LINE)},{paren_line},{n1 n2 --- addr count},{},{
Convert the line number forthcode({n1}) and the screen forthvar({n2}) to the disc buffer
address containing the data. A count of 64 indicates the full line
text length.},{{-LINE}})
HEADER({(LINE)},{PLINE},{DOCOL})
        DC      TOR
        DC      LIT,64
        DC      BBUF
        DC      SSMOD
        DC      FROMR
        DC      BSCR
        DC      STAR
        DC      PLUS
        DC      BLOCK
        DC      PLUS
        DC      LIT,64
        DC      SEMIS
;
worddoc({SCREEN},{.LINE},{dot_line},{line scr --},{},{
Print on the terminal device, a line of text from the disc by its
line and screen number. Trailing blanks are suppressed,
which leads to a tremendous speed advantage on modern glass tty's. },{})
HEADER({.LINE},{DLINE},{DOCOL})
        DC      PLINE
        DC      DTRAI
        DC      LTYPE
        DC      SEMIS
;
worddoc({ERROR},{MESSAGE},{message},{n --},{},{
Print on the selected output device the text of line forthvar({n}) relative to
screen 4 of drive 0. forthvar({n}) may be positive or negative. forthcode({MESSAGE}) may be
used to print incidental text such as report headers. If forthcode({WARNING}) is
zero, the message will simply be printed as a number (disc
unavailable).},{})
HEADER({MESSAGE},{MESS},{DOCOL})
        DC      WARN
        DC      FETCH
        _0BRANCH(MESS1) ;{IF}
        DC      DDUP
        _0BRANCH(MESS2) ;{IF}
        DC      LIT,4
        DC      DLINE
        DC      SPACE   ;{ENDIF}
MESS2:  _BRANCH(MESS3)  ;{ELSE}
MESS1:  DC      PDOTQ
        _STRING({MSG # })
        DC      DOT     ;{ENDIF}
MESS3:  DC      SEMIS
        PAGE
        ;{ At line     LINE ~3000}
;{--------------------------------------------}
;
;{      8086/88 PORT FETCH AND STORE}
;
;{--------------------------------------------}
;
CODE_HEADER({PC@},{PCFET})
;{ FETCH CHARACTER (BYTE) FROM PORT}
        POP     DX      ;{ PORT ADDR}
        XOR     AX,AX
        IN      AL,{DX}   ;{ BYTE INPUT}
        _PUSH
;
CODE_HEADER({PC!},{PCSTO})
;{ STORE CHARACTER (BYTE) AT PORT}
        POP     DX      ;{PORT ADDR}
        POP     AX      ;{DATA}
        OUT     {DX},AL   ;{ BYTE OUTPUT}
        _NEXT
;
CODE_HEADER({P@},{PFET})
;{ FETCH WORD FROM PORT}
        POP     DX      ;{PORT ADDR}
        IN      AX,{DX}   ;{WORD INPUT}
        _PUSH
;
CODE_HEADER({P!},{PSTO})
;{ STORE WORD AT PORT}
        POP     DX      ;{PORT ADDR}
        POP     AX      ;{DATA}
        OUT     {DX},AX   ;{WORD OUTPUT}
        _NEXT
;
HEADER({USE},{USE},{DOVAR})
        ;{ADDR OF NEXT BUFR TO USE}
        DC BUF1
;
HEADER({PREV},{PREV},{DOVAR})
;{       ;ADDR OF PREV USED BUFR}
        DC      BUF1
        PAGE
HEADER({#BUFF},{NOBUF},{DOCON})
        ;{NO. OF BUFFERS}
        DC      NBUF
;
HEADER({+BUF},{PBUF},{DOCOL})
        DC      LIT,(KBBUF+2*CW)
        DC      PLUS,LDUP
        DC      LIMIT,EQUAL
        _0BRANCH(PBUF1)
        DC      DROP,FIRST
PBUF1:  DC      LDUP,PREV
        DC      FETCH,LSUB
        DC      SEMIS
;
HEADER({UPDATE},{UPDAT},{DOCOL})
        DC      PREV
        DC      FETCH,FETCH
        DC      LIT,BMASK
        DC      LOR
        DC      PREV,FETCH
        DC      STORE,SEMIS
;
HEADER({EMPTY-BUFFERS},{MTBUF},{DOCOL})
        DC      FIRST
        DC      LIMIT,OVER
        DC      LSUB,LERASE
        DC      SEMIS
        PAGE
;
HEADER({BUFFER},{BUFFE},{DOCOL})
;{ NOTE: THIS WORD WON'T WORK IF ONLY USING SINGLE BUFFER}
        DC      USE
        DC      FETCH,LDUP
        DC      TOR
BUFF1:  DC      PBUF
        _0BRANCH(BUFF1)
        DC      USE,STORE
        DC      RR,FETCH
        DC      ZLESS
        _0BRANCH(BUFF2)
        DC      RR,CELLP
        DC      RR,FETCH
        DC      LIT,7FFFH
        DC      LAND,ZERO
        DC      RSLW
BUFF2:  DC      RR,STORE
        DC      RR,PREV
        DC      STORE,FROMR
        DC      CELLP,SEMIS
        PAGE
HEADER({BLOCK},{BLOCK},{DOCOL})
        DC      LIT, PMASK, LAND
        DC      OFSET
        DC      FETCH,PLUS
        DC      TOR,PREV
        DC      FETCH,LDUP
        DC      FETCH,RR
        DC      LSUB
        DC      LDUP,PLUS
        _0BRANCH(BLOC1)
BLOC2:  DC      PBUF,ZEQU
        _0BRANCH(BLOC3)
        DC      DROP,RR
        DC      BUFFE,LDUP
        DC      RR,ONE
        DC      RSLW
        DC      LCELL,LSUB
BLOC3:  DC      LDUP,FETCH
        DC      RR,LSUB
        DC      LDUP,PLUS
        DC      ZEQU
        _0BRANCH(BLOC2)
        DC      LDUP,PREV
        DC      STORE
BLOC1:  DC      FROMR,DROP
        DC      CELLP,SEMIS
        PAGE
HEADER({FLUSH},{FLUSH},{DOCOL})
        DC      NOBUF,ONEP
        DC      ZERO,XDO
FLUS1:  DC      ZERO,BUFFE
        DC      DROP
        _LOOP(FLUS1)
        DC      SEMIS
;
worddoc({SCREEN},{LOAD},{load},{n ---},{L0},{
Begin interpretation of screen forthvar({n}) .
Loading will terminate at the end of the screen or at forthcode({;S}) .},
{{-->}})
HEADER({LOAD},{LOAD},{DOCOL})
        DC      BLK
        DC      FETCH,TOR
        DC      LIN,FETCH
        DC      TOR,ZERO
        DC      LIN,STORE
        DC      BSCR,STAR
        DC      BLK,STORE       ;{BLK <- SCR * B/SCR}
        DC      INTER   ;{INTERPRET FROM OTHER}
SCREEN: DC      FROMR,LIN
        DC      STORE
        DC      FROMR,BLK
        DC      STORE
        DC      SEMIS
;
worddocsafe({SCREEN},{-->},{next_screen},{},{P,L0},{
Continue interpretation with the next disc screen.},{{LOAD}})
HEADER({-->},{ARROW},{DOCOL},1)
        DC      QLOAD
        DC      ZERO
        DC      LIN
        DC      STORE
        DC      BSCR
        DC      BLK
        DC      FETCH
        DC      OVER
        DC      LMOD
        DC      LSUB
        DC      BLK
        DC      PSTOR
        DC      SEMIS
        PAGE
;
;{ Generic call on BIOS. A boon for experimenters.}
;{ Because there is no such thing as a variable interrupt:}
;{ THIS IS SELF MODIFYING CODE! NOT REENTRANT! DO NOT PUT IN ROM!}
;{ BEWARE OF THE SOFTWARE POLICE!}
CODE_HEADER({BIOS},{BIOS})
        POP     AX      ;{ Function code}
        ;{ Once we are more acknowledgable, put segment overwrite here.}
        MOV     _BYTE_PTR([RQBIOS+1]),AL ;{ Patch the code.}
        POP     DX
        POP     CX
        POP     BX
        POP     AX
        PUSH     SI      ;{ Save Forth registers. NEEDED? }
        PUSH     BP
        XCHG    SI,AX   ;{ Save AX in (already free) SI}
        _SWITCH_1_({JMPHERE_FROM_FORTH})
        XCHG    {SI},{AX}
RQBIOS: INT(0)          ;{ Request number to be overwritten.}
        PUSHF      ;{ Save status into DI }
        POP     {DI}
        XCHG    {SI},{AX}  ;{ Save AX in (still free) SI     }
        _SWITCH_1_({JMPHERE_FROM_OS})
        XCHG    SI,AX
        POP     BP      ;{ Restore Forth registers. NEEDED? }
        POP     SI      ;{INTERPRETER PNTR}
        PUSH     AX
        PUSH     BX
        PUSH     CX
        PUSH     DX
        PUSH     DI     ;{ i.e. flags }
        _NEXT
_SWITCH_1_({SPSAVE: DC       0H})
;{ SELF MODIFYING CODE ENDS HERE! YOU HAVE BEEN WARNED!}

;{ Generic call on MSDOS/MS WIndows2000}
;{ This works in a standalone Forth (if started from MSDOS).}
CODE_HEADER({BDOS},{BDOS})
        POP     DX
        POP     CX
        POP     BX
        POP     AX
        PUSH     SI      ;{ Save Forth registers. NEEDED? }
        PUSH     BP
        XCHG    SI,AX   ;{ Save AX in (already free) SI}
        _SWITCH_1_({JMPHERE_FROM_FORTH})
        XCHG    {SI},{AX}
        INT     21H
        PUSHF      ;{ Save status into DI }
        POP     {DI} ;{ Not EDI! }
        XCHG    {SI},{AX}   ;{ Save AX in (still free) SI     }
        _SWITCH_1_({JMPHERE_FROM_OS})
;        XCHG    {SI},{AX}   ; This is more correct actually.
        XCHG    SI,AX
        POP     BP      ;{ Restore Forth registers. NEEDED? }
        POP     SI      ;{INTERPRETER PNTR}
        PUSH     AX
        PUSH     BX
        PUSH     CX
        PUSH     DX
        PUSH     DI     ;{ i.e. flags }
        _NEXT

_HOSTED_LINUX_1_({
;{ Generic call on LINUX }
CODE_HEADER({LINOS},{LINOS})
        POP     AX        ; Function number
        POP     DX        ; Third parameter, if any
        POP     CX        ; Second parameter, if any
        POP     BX        ; First parameter.
        INT     80H
        _PUSH     ; Positive means okay. Negative means -errno.
});_END___1__({ _HOSTED_LINUX_1_})

             ;{ SVGA-modus 43*132 char's}
HEADER({SMALL},{SMALL},{DOCOL})
             DC      LIT, 4F02H, LIT, 010AH, ZERO, ZERO
             DC      LIT, 0010H, BIOS
             DC      DROP, DROP, DROP, DROP, DROP, SEMIS
             ;{                          ^ MUST BE 4FH}
        PAGE
;{------------------------------------}
;{       SYSTEM DEPENDANT CHAR I/O}
;{------------------------------------}
_MODERN_1_({
;{ All I/O goes through the modern (unix-borrowed)}
;{ file I/O.    }
HEADER({TYPE},{LTYPE},{DOCOL})
        DC      LDUP, LOUT, PSTOR
        DC      TOR     ; { Count.}
_BITS32_1_({; MSDOS cannot handle a 32 bits address: copy the buffer.})
_BITS32_1_({        DC      LIT, TYPEBUF, RR, LCMOVE })
_BITS32_1_({        DC      LIT, TYPEBUF })
        DC      TOR     ; { Buffer address.}
        DC      LIT,04000H ;{ Write file or device }
        DC      ONE     ;{ Handle}
        DC      FROMR, FROMR, SWAP
         _SWITCH_1_({DC   LIT, M4_SWITCHOFFSET, PLUS, })
        DC      BDOS, DROP ;{ ignore error }
        DC      DROP, DROP, DROP, DROP
        DC      SEMIS
_BITS32_1_({TYPEBUF:  _RESB    0100H })
;
HEADER({EMIT},{EMIT},{DOCOL})
        DC      LDUP,LIT,LF,EQUAL
        _0BRANCH(EMIT1)
        DC      LIT,ACR,EMIT
        DC      ZERO,LOUT,STORE
EMIT1:
        DC      SPFET, ONE, LTYPE
        DC      DROP
        DC      SEMIS
;
 HEADER({EXPECT},{EXPEC},{DOCOL})
_BITS32_1_({; MSDOS cannot handle a 32 bits address: use the buffer.})
_BITS32_1_({        DC      LIT, EXPECTBUF})
_BITS16_1_({        DC      OVER})
        DC      TOR, TOR
        DC      LIT,03F00H ;{ Write file or device }
        DC      ZERO     ;{ Handle}
        DC      FROMR, FROMR
         _SWITCH_1_({DC   LIT, M4_SWITCHOFFSET, PLUS, })
        DC      BDOS, DROP ;{ ignore error }
        DC      DROP, DROP, DROP ;{ Leave count}
        DC      TWO, LSUB ;{ Rid of 0D0A}
_BITS32_1_({; Copy the buffer.})
_BITS32_1_({        DC      TOR, LDUP, LIT, EXPECTBUF, SWAP, RR, LCMOVE, FROMR })
        DC      PLUS, ZERO, SWAP, CSTOR
        DC      SEMIS
_BITS32_1_({EXPECTBUF:  _RESB    0100H })
;
HEADER({KEY},{KEY},{DOCOL})
        DC         LIT, 1000H, ZERO, ZERO, ZERO
        DC         LIT, 0016H, BIOS
        DC         DROP,DROP,DROP,DROP
        DC         LIT, 00FFH, LAND, SEMIS
;
HEADER({?TERMINAL},{QTERM},DOCOL)
        DC      LIT,0B00H
        DC      ZERO,ZERO,ZERO
        DC      BDOS, DROP ;{ ignore error }
        DC      DROP, DROP, DROP
        DC      LIT, 01H, LAND
        DC      SEMIS
;

});_END___1__({_MODERN_1_})
_LINUX_C_1_({
;{ All I/O goes through the modern device (unix-standard)}
;{ device I/O. Code fields are filled in during bootup. }
;{ Lower case labels starting with "c_.." are c-supplied facilities.}
CODE_HEADER({TYPE},{LTYPE})
        CALL    c_type
        LEA     ESP,[ESP+CELLS(2)]    ; remove input
        _NEXT
;
HEADER({EMIT},{EMIT},{DOCOL})
        DC      SPFET, ONE, LTYPE
        DC      DROP
        DC      SEMIS
;
CODE_HEADER({EXPECT},{EXPEC})
        CALL    c_expec
        LEA     ESP,[ESP+CELLS(2)]    ; remove input
        _NEXT
;
CODE_HEADER({KEY},{KEY})
        CALL    c_key
        PUSH    EAX
        _NEXT
;
CODE_HEADER({?TERMINAL},{QTERM})
        CALL    c_qterm
        PUSH    EAX
        _NEXT
;
});_END___1__({_LINUX_C_1_})
_LINUX_N_1_({
;{ All I/O goes through the modern device (unix-standard)}
;{ device I/O. Code fields are filled in during bootup. }
;{ Lower case labels starting with "c_.." are c-supplied facilities.}
HEADER({?LINUX-ERROR},{QLERR},{DOCOL})
        DC      LDUP, ZERO, MIN, SWAP ; ( errno -- error flag, errno)
        DC      LIT, 64, PLUS   ; Project onto SCREEN 4..7.
        DC      QERR  ; Make errno's count from the top.
        DC      SEMIS
;
HEADER({TERMIO},{TERMIO},{DOVAR})
        _RESB    SIZE_TERMIO     ; Filled induring cold boot.
;
;          ( len b -- )
;  Set the terminal length to len and toggle the c_lflag field with b
HEADER({SET-TERM},{STTERM},{DOCOL})
        DC      LIT, _PARAMETER_FIELD(TERMIO)+CELLS(3)
        DC      SWAP, TOGGL
        DC      LIT, _PARAMETER_FIELD(TERMIO)+CELLS(4)+7
        DC      CSTOR
        DC      ZERO, LIT, TCSETS, TERMIO
        DC      LIT, ioctl, LINOS, QLERR
        DC      SEMIS
; Alternative: if no terminal, this is copied over above code during boot
STTERM_A:
        DC      DROP, DROP
        DC      SEMIS
STTERM_B:

;
HEADER({TYPE},{LTYPE},{DOCOL})
        DC      ONE, ROT, ROT   ; filedescriptor 1 for standard output.
        DC      LIT, write
        DC      LINOS
        DC      DROP
        DC      SEMIS
;
HEADER({EMIT},{EMIT},{DOCOL})
        DC      SPFET, ONE, LTYPE
        DC      DROP
        DC      SEMIS
;
HEADER({EXPECT},{EXPEC},{DOCOL})
        DC      ZERO    ; filedescriptor 0 for standard input.
        DC      ROT
        DC      LDUP, TOR   ; Remember start of buffer
        DC      ROT
        DC      LIT, read
        DC      LINOS
        DC      ONE, LSUB, ZERO, MAX ; Position of CR (or 0 for failure)
        DC      FROMR, PLUS          ; Address
        DC      ZERO,SWAP,CSTOR      ; Erase it
        DC      SEMIS
        _RESB    CELLS(4)        ; Room for patching
; Alternative: if no terminal, this is copied over above code during boot
EXPEC_A:
        DC      OVER, OVER, ONEP, LERASE
        DC      OVER, PLUS, SWAP     ; Bounds
        DC      XDO
EXPEC1: DC      KEY, LDUP, LIT, LF
        DC      EQUAL
        _0BRANCH(EXPEC2)
        DC      DROP
        DC      LLEAV
        _BRANCH(EXPEC3)
EXPEC2: DC      IDO,CSTOR
EXPEC3: _LOOP(EXPEC1)
        DC      SEMIS
EXPEC_B:
;
HEADER({KEY},{KEY},{DOCOL})
        DC      ONE, LIT, RAWIO, STTERM ; Do not wait for CR
        DC      ZERO, SPFET     ; Expect single key on stack
        DC      ZERO            ; Standard input
        DC      SWAP, ONE       ; Buffer and length
        DC      LIT, read, LINOS, DROP ; Retain char, drop count/error
        DC      ONE, LIT, RAWIO, STTERM       ; Toggle back
        DC      SEMIS
;
HEADER({?TERMINAL},{QTERM},{DOCOL})
        ; Return immediately even for zero characters available
        DC      ZERO, LIT, RAWIO, STTERM
        DC      ZERO, SPFET     ; Expect single key on stack
        DC      ZERO            ; Standard input
        DC      SWAP, ONE       ; Buffer and length
        DC      LIT, read, LINOS
        DC      SWAP, DROP      ; Retain only count
        DC      ONE, LIT, RAWIO, STTERM       ; Toggle back
        DC      SEMIS
;
});_END___1__({_LINUX_N_1_})
_CLASSIC_1_({
;{ All I/O goes through the 3 assembler functions}
;{ CI CHO CSTAT. They could be collapsed into the }
;{ code definitions, but this way they are available}
;{ to be called from code.}
HEADER({EXPECT},{EXPEC},{DOCOL})
        DC      OVER
        DC      PLUS
        DC      OVER
        DC      XDO     ;{ DO                    }
EXPE1:  DC      KEY
        DC      LDUP
        DC      RUBOUT
        DC      FETCH
        DC      EQUAL
        _0BRANCH(EXPE2) ;{ IF                    }
        DC      DROP
        DC      LDUP
        DC      IDO
        DC      EQUAL
        DC      LDUP
        DC      FROMR
        DC      TWO     ;{ Remove last 2 chars }
        DC      LSUB
        DC      PLUS
        DC      TOR
        _0BRANCH(EXPE6) ;{ IF                    }
        DC      LIT
        DC      BELL
        _BRANCH(EXPE7)  ;{ ELSE                  }
EXPE6:  DC      LIT
        DC      BSOUT   ;{ ENDIF                 }
EXPE7:  _BRANCH(EXPE3)  ;{ ELSE                  }
EXPE2:  DC      LDUP
        DC      LIT,0DH
        DC      EQUAL
        _0BRANCH(EXPE4) ;{ IF}
        DC      LLEAV
        DC      DROP
        DC      BLS
        DC      ZERO
        _BRANCH(EXPE5)  ;{ ELSE}
EXPE4:  DC      LDUP    ;{ ENDIF}
EXPE5:  DC      IDO
        DC      CSTOR
        DC      ZERO
        DC      IDO
        DC      ONEP
        DC      STORE   ;{ ENDIF}
EXPE3:  DC      EMIT
        _LOOP(EXPE1)    ;{ LOOP}
        DC      DROP
        DC      SEMIS
;
CODE_HEADER({KEY},{KEY})
        CALL    CI      ;{CONSOLE INPUT}
        MOV     AH,0    ;{MAKE 16 BITS}
        _PUSH   ;{SAVE KEY VALUE}
CI      PROC    NEAR
        MOV     AH,0    ;{ READ CHAR FUNCTION}
        INT     16H
        RET
_ENDP CI      ENDP
;
HEADER({TYPE},{LTYPE},{DOCOL})
        DC      DDUP
        _0BRANCH(TYPE1) ;{ IF}
        DC      OVER
        DC      PLUS
        DC      SWAP
        DC      XDO     ;{ DO}
TYPE2:  DC      IDO
        DC      CFET
        DC      LIT,7FH
        DC      LAND    ;{STRIP 80H FROM LAST CHAR}
        DC      EMIT
        _LOOP(TYPE2)    ;{ LOOP}
        _BRANCH(TYPE3)  ;{ ELSE}
TYPE1:  DC      DROP    ;{ ENDIF}
TYPE3:  DC      SEMIS
;
HEADER({EMIT},{EMIT},{DOCOL})
        DC      PEMIT
        DC      ONE,LOUT
        DC      PSTOR,SEMIS
PEMIT   DC      $+CELLS(1)
        POP     AX      ;{GET CHAR}
        CMP     AL,LF   ;{ Precede ^J with ^M}
        JNE     PEMIT1
        MOV     AL,ACR
        CALL    CHO
        MOV     AL,LF
        ;{ Bizar, without the colon MASM reports error.}
PEMIT1: CALL    CHO    ;{CHAR OUTPUT}
        _NEXT
CHO     PROC    NEAR
        PUSH    DX
        PUSH    AX      ;{SAVE CHAR}
        MOV     DL,AL   ;{CHAR TO WRITE}
        MOV     AH,2    ;{CHAR OUT FUNCTION}
        INT     21H     ;{DOS}
        POP     AX
        POP     DX
        RET
_ENDP CHO     ENDP
;
CODE_HEADER({?TERMINAL},{QTERM})
        JMP     PQTER
PQTER:  CALL    CSTAT   ;{TEST FOR KEY}
        OR      AL,AL   ;{ANY KEY?}
        JZ      PQTER1  ;{NO}
        MOV     AL,1    ;{TRUE = CHAR FOUND}
PQTER1: MOV     AH,0    ;{MAKE 16 BITS}
        _PUSH   ;{SAVE STATUS}
;
CSTAT   PROC    NEAR
        PUSH    DX
        MOV     DX,0FFH
        MOV     AX,0600H
        INT     21H
        POP     DX
STATRT: RET
_ENDP CSTAT   ENDP
});_END___1__({_CLASSIC_1_})
_USEBIOS_1_({
HEADER({EXPECT},{EXPEC},{DOCOL})
        DC      OVER
        DC      PLUS
        DC      OVER
        DC      XDO     ;{ DO                    }
EXPE1:  DC      KEY
        DC      LDUP
        DC      RUBOUT
        DC      FETCH
        DC      EQUAL
        _0BRANCH(EXPE2) ;{ IF                    }
        DC      DROP
        DC      LDUP
        DC      IDO
        DC      EQUAL
        DC      LDUP
        DC      FROMR
        DC      TWO     ;{ Remove last 2 chars }
        DC      LSUB
        DC      PLUS
        DC      TOR
        _0BRANCH(EXPE6) ;{ IF                    }
        DC      LIT
        DC      BELL
        _BRANCH(EXPE7)  ;{ ELSE                  }
EXPE6:  DC      LIT
        DC      BSOUT   ;{ ENDIF                 }
EXPE7:  _BRANCH(EXPE3)  ;{ ELSE                  }
EXPE2:  DC      LDUP
        DC      LIT,0DH
        DC      EQUAL
        _0BRANCH(EXPE4) ;{ IF}
        DC      LLEAV
        DC      DROP
        DC      BLS
        DC      ZERO
        _BRANCH(EXPE5)  ;{ ELSE}
EXPE4:  DC      LDUP    ;{ ENDIF}
EXPE5:  DC      IDO
        DC      CSTOR
        DC      ZERO
        DC      IDO
        DC      ONEP
        DC      STORE   ;{ ENDIF}
EXPE3:  DC      EMIT
        _LOOP(EXPE1)    ;{ LOOP}
        DC      DROP
        DC      SEMIS
;
HEADER({KEY},{KEY},{DOCOL})
             DC         LIT, 1000H, ZERO, ZERO, ZERO
             DC         LIT, 0016H, BIOS
             DC         DROP,DROP,DROP,DROP
             DC         LIT, 00FFH, LAND, SEMIS

HEADER({TYPE},{LTYPE},{DOCOL})
             DC      DDUP
             _0BRANCH(TYPE1)
             DC      OVER,   PLUS
             DC      SWAP,   XDO
TYPE2:       DC      IDO,    CFET,    EMIT
             _LOOP(TYPE2)
             _BRANCH(TYPE3)
TYPE1:       DC      DROP
TYPE3:       DC      SEMIS
;{            }
HEADER({EMIT},{EMIT},{DOCOL})
             DC      ONE,LOUT,PSTOR
             DC      LIT, 07FH, LAND
             DC      LDUP,LIT,LF,EQUAL
             _0BRANCH(EMIT1)
             DC      LIT,ACR,PEMIT
             DC      ZERO,LOUT,STORE
EMIT1:       DC      PEMIT
             DC      SEMIS
;{            }
HEADER({(EMIT)},{PEMIT},{DOCOL})
             DC      LIT, 0E00H, PLUS, ZERO, ZERO, ZERO
             DC      LIT, 0010H, BIOS
             DC      DROP, DROP, DROP, DROP, DROP, SEMIS

;
HEADER({?TERMINAL},{QTERM},{DOCOL})
             DC      LIT, 01100H, ZERO, ZERO, ZERO
             DC      LIT, 016H, BIOS
             DC      LIT, 040H, LAND, ZEQU, TOR
             DC      DROP, DROP, DROP, DROP
             DC      FROMR, SEMIS

             ;{ Output to PRN }
HEADER({POUT},{POUT},{DOCOL})
             DC      ZERO, ZERO, ZERO
             DC      LIT, 0017H, BIOS
             DC      DROP, DROP, DROP, DROP, DROP, SEMIS
;
});_END___1__({_USEBIOS_1_})
        PAGE
;{------------------------------------}
;{       SYSTEM DEPENDANT DISK I/O}
;{------------------------------------}

_RWFILE_1_({
HEADER({BLOCK-FILE},{BLFL},{DOVAR})
        _STRING({BLOCKS.BLK})
        _RESB(20)               ;{ Allow for some path}
;
HEADER({BLOCK-HANDLE},{BHAN},{DOVAR})
        DC      0
;
HEADER({DISK-ERR},{DERR},{DOCOL})
        DC      ONE, LAND
        DC      LIT, 08H, QERR
        DC      SEMIS
;
HEADER({BLOCK-INIT},{BLINI},{DOCOL})
        DC      LIT, 03D02H    ;{ Open file for read/write}
        DC      ZERO,ZERO
        DC      BLFL
        DC      COUNT, OVER, PLUS
        DC      ZERO, SWAP, CSTOR  ;{ Zero ended as required}
         _SWITCH_1_({DC   LIT, M4_SWITCHOFFSET, PLUS, })
        DC      BDOS
        DC      ONE, LAND, ZEQU, WARN, STORE
        DC      DROP, DROP, DROP
        DC      BHAN, STORE
        DC      SEMIS
;
HEADER({BLOCK-EXIT},{BLEXI},{DOCOL})
        DC      LIT, 03E00H    ;{ Close file }
        DC      ZERO, WARN, STORE
        DC      BHAN, FETCH
        DC      ZERO, ZERO
        DC      BDOS, DERR
        DC      DROP, DROP, DROP, DROP
        DC      SEMIS
;
;{       Block #, Handle}
HEADER({SEEK},{SEEK},{DOCOL})
        DC      SWAP, TOR, TOR
        DC      LIT, 04200H     ;{ Seek from start}
        DC      FROMR           ;{ Handle}
        DC      FROMR, BBUF
        _BITS16_1_({DC      USTAR,SWAP})    ;{ L.S. ON TOP}
        _BITS32_1_({DC      STAR, LIT, 10000H, SLMOD,SWAP})    ;{ L.S. ON TOP}
        DC      BDOS, DERR
        DC      DROP, DROP, DROP, DROP
        DC      SEMIS
;
;{      ( ADDR  BLK#  FLAG (0=W, 1=R) --- )}
HEADER({R/W},{RSLW},{DOCOL})
        _0BRANCH(RSLW1)
        DC      LIT, 03F00H     ;{ Disk read }
        _BRANCH(RSLW2)
RSLW1:  DC      LIT, 04000H     ;{ Disk write}
RSLW2:  DC      TOR
        DC      BHAN, FETCH, SEEK ;{ That's done}
        DC      FROMR, SWAP, TOR
        DC      BHAN, FETCH
        DC      BBUF
        DC      FROMR           ;{ ADDR}
        _SWITCH_1_({DC   LIT, M4_SWITCHOFFSET, PLUS, })
        DC      BDOS, DERR
        DC      DROP, DROP, DROP
        DC      BBUF, LSUB, DERR
        DC      SEMIS

});_END___1__({_RWFILE_1_})
_LINUX_N_1_({
HEADER({BLOCK-FILE},{BLFL},{DOVAR})
        _STRING({BLOCKS.BLK})
        DB      0       ; { Easier on linux}
        _RESB(19)               ;{ Allow for some path}
;
HEADER({BLOCK-HANDLE},{BHAN},{DOVAR})
        DC      0
;
HEADER({DISK-ERROR},{DERR},{DOVAR})
        DC      0
;
HEADER({BLOCK-INIT},{BLINI},DOCOL)
        DC      BLFL, ONEP
        DC      LIT, O_RDWR
        DC      ZERO
        DC      LIT, open
        DC      LINOS
        DC      LDUP, BHAN, STORE
        DC      ZERO, MIN      ; 0/-errno
        DC      LDUP, DERR, STORE
        DC      ZEQU, WARN, STORE
        DC      SEMIS
;
HEADER({BLOCK-EXIT},{BLEXI},DOCOL)
        DC      ZERO, WARN, STORE
        DC      BHAN, FETCH
        DC      ZERO,ZERO
        DC      LIT, close
        DC      LINOS
        DC      ZERO, MIN      ; 0/-errno
        DC      DERR, STORE
        DC      SEMIS
;
;{      ( ADDR  BLK#  FLAG (0=W, 1=R) --- )}
HEADER({R/W},{RSLW},{DOCOL})
        DC      TOR
        DC      BBUF, STAR
        DC      BHAN, FETCH, SWAP
        DC      LIT, SEEK_SET
        DC      LIT, lseek
        DC      LINOS
        DC      ZERO, MIN      ; 0/-errno
        DC      LDUP, DERR, STORE
        _0BRANCH( RSLW1 )
        DC      FROMR, DROP
        _BRANCH( RSLW2 )
RSLW1:
        DC      BHAN, FETCH, SWAP
        DC      BBUF
        DC      FROMR
        _0BRANCH( RSLW3 )
        DC     LIT, read
        _BRANCH( RSLW4 )
RSLW3:
        DC      LIT, write
RSLW4:  DC      LINOS
        DC      ZERO, MIN      ; 0/-errno
        DC      DERR, STORE
RSLW2:  DC      SEMIS

});_END___1__({_LINUX_N_1_})
_LINUX_C_1_({
HEADER({BLOCK-FILE},{BLFL},{DOVAR})
        _STRING({BLOCKS.BLK})
        _RESB(20)               ;{ Allow for some path}
;
HEADER({DISK-ERROR},{DERR},{DOVAR})
        DC      0
;
CODE_HEADER({BLOCK-INIT},{BLINI})
        XOR     AX,AX
        MOV     AL,[_PARAMETER_FIELD(BLFL)]
        MOV     BX,_PARAMETER_FIELD(BLFL)+1
        PUSH    BX
        PUSH    AX
        CALL    c_block_init
        MOV     [_PARAMETER_FIELD(DERR)],AX
        LEA     ESP,[ESP+CELLS(2)]    ; remove input
        _NEXT
;
CODE_HEADER({BLOCK-EXIT},{BLEXI})
        CALL    c_block_exit
        _NEXT
;
;{      ( ADDR  BLK#  FLAG (0=W, 1=R) --- )}
CODE_HEADER({R/W},{RSLW})
        CALL c_rslw
        MOV     [_PARAMETER_FIELD(DERR)],AX
        LEA     ESP,[ESP+CELLS(3)]    ; remove input
        _NEXT

});_END___1__({_LINUX_C_1_})
_CLASSIC_1_({
;
WERR    DB      'DISK WRITE ERROR  $'
RERR    DB      'DISK READ ERROR   $'
;
SBLK    PROC    NEAR
;{      ( ADDR  BLK# -- )}
;{      THIS ROUTINE WILL PREPARE PARAMETERS FOR DISK I/O}
;{       DRIVE, ADDRESS, LENGTH, SEC#}
        POP     DI      ;{SAVE RETURN}
        POP     AX      ;{BLK #}
        MOV     CX,SPB
        MUL     CX   ;{SEC #}
        MOV     DX,AX
        MOV     AL,DRIVE    ;{SELECT DR A}
        POP     BX      ;{TRANSFER ADDR}
        PUSH    DI      ;{GET THAT RETURN BACK}
        RET
_ENDP SBLK    ENDP
;
CODE_HEADER({RBLK},{RBLK})
        CALL    SBLK
        PUSH    SI      ;{PRESERVE INTERPRETER POINTER}
        PUSH    BP      ;{RETURN POINTER}
        INT     25H
        JNC     DOK     ;{NO ERRORS}
        LEA     DX,[RERR]
        JMP     DERR
;
CODE_HEADER({WBLK},{WBLK})
        CALL    SBLK
        PUSH    SI
        PUSH    BP
        INT     26H
        JNC     DOK
        LEA     DX,[WERR]
DERR:   PUSH    AX
        MOV     AH,9    ;{STRING WRITE FNCN}
        INT     21H
        POP     AX      ;{ERROR CODE IN AX}
;{  WRITE ERROR MESSAGE HERE}
;
DOK:    POPF            ;{POP FLAGS}
        POP     BP      ;{RETURN STACK}
        POP     SI      ;{INTERPRETER PNTR}
        _NEXT
;
;
;{      ( ADDR  OFFSET+BLK#  FLAG (0=W, 1=R) --- )}
HEADER({R/W},{RSLW},{DOCOL})
         _SWITCH_1_({DC   ROT, LIT, M4_SWITCHOFFSET, PLUS, ROT, ROT})
        _0BRANCH(RSLW1)
        DC      RBLK
        _BRANCH(RSLW2)
RSLW1:  DC      WBLK
RSLW2:  DC      SEMIS
});_END___1__({_CLASSIC_1_})
_USEBIOS_1_({
;
HEADER({DRIVE},{DRIVE},{DOVAR})
      DC  0
;
HEADER({RECORD},{REC},{DOVAR})
      DC  0
;
HEADER({SEC/BLK},{SPBLK},{DOCON}) ;{# SECTORS/BLOCK}
        DC      SPB
;
HEADER({DISK-ERROR},{DSKERR},{DOVAR})
      DC  0
});_END___1__({_USEBIOS_1_})
;
_RWFD_1_({
;{  (Command, address, sec# -- )}
HEADER({SEC-RW},{SECRW},{DOCOL})
        DC      LIT, SPT*HEADS, SLMOD   ;{ Juggle #sec, #head, #track}
        DC      SWAP, LIT, SPT, SLMOD, TOR
        DC      SWAP, LIT, 0100H, STAR, PLUS, ONEP
        DC      FROMR, LIT, 0100H, STAR  ;{ .. to arrive at CX, DX}
        DC      LIT, 13H, BIOS
        DC      ONE, LAND, DSKERR, PSTOR
        DC      DROP, DROP, DROP, DROP
        DC      SEMIS
;
;{      ( ADDR  OFFSET+BLK#  FLAG (0=W, 1=R) --- )}
HEADER({R/W},{RSLW},{DOCOL})
         _SWITCH_1_({DC   ROT, LIT, M4_SWITCHOFFSET, PLUS, ROT, ROT})
        DC      ZERO, DSKERR, STORE
        _0BRANCH(RSLW1)
        DC      LIT, 0201H      ;{ Read (AH) one (AL) sector }
        _BRANCH(RSLW2)
RSLW1:  DC      LIT, 0301H      ;{ Write (AH) one (AL) sector }
RSLW2:  DC      SWAP
        DC      SPBLK,  STAR
        DC      SPBLK,  OVER, PLUS
        DC      SWAP,   XDO
RSLW0:
        DC      SWAP, TDUP
        DC      IDO
        DC      SECRW
        DC      LIT, BPS, PLUS, SWAP
        _LOOP(RSLW0)
        DC      DROP, DROP
        DC      DSKERR, FETCH,     DDUP
        _0BRANCH(RSLW5)              ;{OK}
        DC      ZLESS
        _0BRANCH(RSLW3)
        DC      LIT,    9   ;{Write error}
        _BRANCH(RSLW4)
RSLW3:  DC      LIT,    8   ;{Read error}
RSLW4:  DC      ZERO,   PREV,   FETCH,     STORE   ;{This  buffer}
                                                   ;{ is no good!}
        DC      QERR
RSLW5:  DC      SEMIS
;
});_END___1__({ _RWFD_1_})
_RWHD_1_({
;{ Warning : if this LBA is used apart from by R/W}
;{ the fields marked default must be restored.}
HEADER({LBAPAR},{LBA},{DOVAR})
        DB      10H, 0      ;{ default    }
        DW      SPB         ;{ default    }
        DW      0           ;{ to be filled with offset}
        DW      SWITCHSEGMENT ;{ default segment}
        ;{ At a hard disk we just read sector from sector 1        }
        DD      0       ;{ sector number}
        DD      0       ;{ M.S. 32 BITS default}

;{ Reads/ writes the block such as in LBA}
CODE_HEADER({RBLK},{RBLK})
        MOV     BX,4200H
        JMP    RWBLK
;
CODE_HEADER({WBLK},{WBLK})
        MOV     BX,4300H
RWBLK:
        POP     AX
        PUSH    SI      ;{PRESERVE INTERPRETER POINTER}
        _SWITCH_1_({ADD     AX, M4_SWITCHOFFSET })
        MOV     SI,AX
        PUSH    BP      ;{RETURN POINTER}
        MOV     DX,0080H  ;{   Disk C                                 }
        _SWITCH_1_({JMPHERE_FROM_FORTH})
        XCHG    BX,AX
        INT     13H
        PUSHF           ;{ REMEMBER STATUS}
        POP     BX
        _SWITCH_1_({JMPHERE_FROM_OS})
        POP     BP      ;{RETURN STACK}
        POP     SI      ;{INTERPRETER PNTR}
        PUSH    BX
        _NEXT

;{      ( ADDR  OFFSET+BLK#  FLAG (0=W, 1=R) --- )}
HEADER({R/W},{RSLW},{DOCOL})
        DC      TOR
        DC      LIT, SPB, STAR  ;{ Sector number}
        DC      SWAP
;       On a big endian machine the following code is 32 bit clean.
;       The 2 m.s. bytes are overwritten by the next store.
         _SWITCH_1_({DC   LIT, M4_SWITCHOFFSET, PLUS })
        DC      LBA, LIT, 4, PLUS, STORE   ; { Address }
        DC      LIT, SWITCHSEGMENT
        DC      LBA, LIT, 6, PLUS, STORE
        DC      LBA, LIT, 8, PLUS, STORE
        DC      LBA
        DC      RR
        _0BRANCH(RSLW1)
        DC      RBLK
        _BRANCH(RSLW2)
RSLW1:  DC      WBLK
RSLW2:  DC      LDUP, DSKERR, STORE

        DC      ONE, LAND, LDUP
        _0BRANCH(RSLW4)
;{        DC      ZERO,   PREV,   FETCH,     STORE   ;This  buffer}
                                                   ;{ is no good!}
RSLW4:
        ;{ Handle error}
        DC      LIT,    9   ;{Write error}
        DC      FROMR, LSUB ;{ 1 less: read error}
        DC      QERR
        DC      SEMIS
;
});_END___1__({ _RWHD_1_})
        PAGE
        ;{ At line     LINE ~3500}
worddocsafe({DICTIONARY},{'},{tick},{--- addr},{P,L0},{
Used in the form: .
forthcode({'}) forthcode({nnnn})
Leaves the parameter field address of dictionary word forthvar({nnnn}).
As a compiler directive, executes in a colon-definition to compile
the address as a literal. If the word is not found after a search of
forthcode({CONTEXT}) and forthcode({CURRENT}) , an appropriate error message is given.},
{{CFA},{LFA},{NFA},{PFA}} )
HEADER({'},{TICK},{DOCOL},1)
        DC      DFIND
        DC      ZEQU
        DC      ZERO
        DC      QERR
        DC      DROP
        DC      LITER
        DC      SEMIS
;
HEADER({FORGET},{FORG},{DOCOL})
        DC      CURR
        DC      FETCH
        DC      CONT
        DC      FETCH
        DC      LSUB
        DC      LIT,18H
        DC      QERR
        DC      TICK
        DC      LDUP
        DC      FENCE
        DC      FETCH
        DC      LESS
        DC      LIT,15H
        DC      QERR
        DC      LDUP
        DC      NFA
        DC      LDP
        DC      STORE
        DC      LFA
        DC      FETCH
        DC      CONT
        DC      FETCH
        DC      STORE
        DC      SEMIS
        PAGE
HEADER({BACK},{BACK},{DOCOL})
        DC      HERE
        DC      LSUB
        DC      COMMA
        DC      SEMIS
;
HEADER({BEGIN},{BEGIN},{DOCOL},1)
        DC      QCOMP
        DC      HERE
        DC      ONE
        DC      SEMIS
;
HEADER({ENDIF},{ENDIFF},{DOCOL},1)
        DC      QCOMP
        DC      TWO     ;{ Magic number}
        DC      QPAIR
        DC      HERE
        DC      OVER
        DC      LSUB
        DC      SWAP
        DC      STORE
        DC      SEMIS
        PAGE
HEADER({THEN},{THEN},{DOCOL},1)
        DC      ENDIFF
        DC      SEMIS
;
worddoc({CONTROL},{DO},{do},{n1 n2 --- (execute)
addr n --- (compile)},{P,C2,L0},{
Occurs in a colon-definition in form:
    forthsamp({DO ... LOOP})

At run time, forthcode({DO}) begins a sequence with repetitive execution
controlled by a loop limit forthvar({n1}) and an index with initial value forthvar({n2}) . forthcode({DO})
removes these from the stack. Upon reaching forthcode({LOOP}) the index is
incremented by one.
Until the new index equals or exceeds the limit, execution loops
back to just after forthcode({DO}) ; otherwise the loop parameters are discarded
and execution continues ahead. Both forthvar({n1}) and forthvar({n2}) are determined at
run-time and may be the result of other operations.
Within a loop forthcode({I}) will copy the current value of the index to the
stack. 

When compiling within the colon definition, forthcode({DO}) compiles forthcode({(DO)}) , leaves
the following address forthvar({addr}) and forthvar({n}) for later error checking.},
{{I},{LOOP},{+LOOP},{LEAVE}})
HEADER({DO},{DO},{DOCOL},1)
        DC      COMP
        DC      XDO
        DC      HERE
        DC      THREE   ;{ Magic number}
        DC      SEMIS
;
worddoc({CONTROL},{LOOP},{loop},{addr n --- (compiling)},{P,C2,L0},{
Occurs in a colon-definition in form:
               forthsamp({DO ... LOOP})
At run-time, forthcode({LOOP}) selectively controls branching back to the
corresponding forthcode({DO}) based on the loop index and limit. The loop index
is incremented by one and compared to the limit. The branch back to
forthcode({DO}) occurs until the index equals or exceeds the limit;
at that time, the parameters are discarded and execution continues
ahead.

At compile-time, forthcode({LOOP}) compiles forthcode({(LOOP)}) and uses forthvar({addr}) to calculate an
offset to forthvar({DO}) . forthvar({n}) is used for error testing.},{})
HEADER({LOOP},{LLOOP},{DOCOL},1)
        DC      THREE   ;{ Magic number}
        DC      QPAIR
        DC      COMP
        DC      XLOOP
        DC      BACK
        DC      SEMIS
;
worddocsafe({CONTROL},{(+LOOP)},{paren_plus_loop},{n ---},{C2},{
The run-time proceedure compiled by forthcode({+LOOP}), which increments the loop
index by n and tests for loop completion.},{{+LOOP}})
HEADER({+LOOP},{PLOOP},{DOCOL},1)
        DC      THREE   ;{ Magic number}
        DC      QPAIR
        DC      COMP
        DC      XPLOO
        DC      BACK
        DC      SEMIS
;
HEADER({UNTIL},{UNTIL},{DOCOL},1)
        DC      ONE
        DC      QPAIR
        DC      COMP
        DC      ZBRAN
        DC      BACK
        DC      SEMIS
;
HEADER({END},{LEND},{DOCOL},1)
        DC      UNTIL
        DC      SEMIS
        PAGE
HEADER({AGAIN},{AGAIN},{DOCOL},1)
        DC      ONE
        DC      QPAIR
        DC      COMP
        DC      BRAN
        DC      BACK
        DC      SEMIS
;
HEADER({REPEAT},{REPEA},{DOCOL},1)
        DC      TOR
        DC      TOR
        DC      AGAIN
        DC      FROMR
        DC      FROMR
        DC      TWO     ;{ Magic number}
        DC      LSUB
        DC      ENDIFF
        DC      SEMIS
;
HEADER({IF},{LIF},{DOCOL},1)
        DC      COMP
        DC      ZBRAN
        DC      HERE
        DC      ZERO
        DC      COMMA
        DC      TWO     ;{ Magic number           }
        DC      SEMIS
        PAGE
HEADER({ELSE},{LELSE},{DOCOL},1)
        DC      TWO     ;{ Magic number }
        DC      QPAIR
        DC      COMP
        DC      BRAN
        DC      HERE
        DC      ZERO
        DC      COMMA
        DC      SWAP
        DC      TWO     ;{ Magic number           }
        DC      ENDIFF
        DC      TWO     ;{ Magic number           }
        DC      SEMIS
;
HEADER({WHILE},{WHILE},{DOCOL},1)
        DC      LIF
        DC      TWOP        ;{ Magic number           }
        DC      SEMIS
        PAGE
HEADER({SPACES},{SPACES},{DOCOL})
        DC      ZERO
        DC      MAX
        DC      DDUP
        _0BRANCH(SPAX1)
        DC      ZERO
        DC      XDO     ;{DO}
SPAX2:  DC      SPACE
        _LOOP(SPAX2)    ;{LOOP}
SPAX1:  DC      SEMIS
;
HEADER({<#},{BDIGS},{DOCOL})
        DC      PAD
        DC      HLD
        DC      STORE
        DC      SEMIS
;
worddoc({NUMBER},{#>},{number_sign_greater},{d --- addr count},{L0},{
Terminates numeric output conversion by dropping forthvar({d}), leaving the text
address and character count suitable for forthcode({TYPE}).},{})
HEADER({#>},{EDIGS},{DOCOL})
        DC      DROP
        DC      DROP
        DC      HLD
        DC      FETCH
        DC      PAD
        DC      OVER
        DC      LSUB
        DC      SEMIS
        PAGE
HEADER({SIGN},{SIGN},{DOCOL})
        DC      ROT
        DC      ZLESS
        _0BRANCH(SIGN1) ;{IF}
        DC      LIT,2DH
        DC      HOLD    ;{ENDIF}
SIGN1:  DC      SEMIS
;
worddoc({NUMBER},{#},{number_sign},{d1 --- d2},{L0},{
Generate from a double number forthvar({d1}), the next ascii character which is
placed in an output string. Result forthvar({d2}) is the quotient after division
by forthcode({BASE}), and is maintained for further processing. Used between forthcode({<#}) 
and forthcode({#>}).},{{#S}} ) 
HEADER({#},{DIG},{DOCOL})
        DC      BASE
        DC      FETCH
        DC      MSMOD
        DC      ROT
        DC      LIT,9
        DC      OVER
        DC      LESS
        _0BRANCH(DIG1)  ;{IF}
        DC      LIT,7
        DC      PLUS    ;{ENDIF}
DIG1:   DC      LIT,30H
        DC      PLUS
        DC      HOLD
        DC      SEMIS
;
worddoc({NUMBER},{#S},{number_sign_s},{d1 --- d2},{L0},{
Generates ascii text in the text output buffer, by the use of forthcode({#}),
until a zero double number forthvar({n2}) results.
Used between forthcode({<#}) and forthcode({#>}).},{})
HEADER({#S},{DIGS},{DOCOL})
DIGS1:  DC      DIG     ;{BEGIN}
        DC      OVER
        DC      OVER
        DC      LOR
        DC      ZEQU
        _0BRANCH(DIGS1) ;{UNTIL}
        DC      SEMIS
;
HEADER({D.R},{DDOTR},{DOCOL})
        DC      TOR
        DC      SWAP
        DC      OVER
        DC      DABS
        DC      BDIGS
        DC      DIGS
        DC      SIGN
        DC      EDIGS
        DC      FROMR
        DC      OVER
        DC      LSUB
        DC      SPACES
        DC      LTYPE
        DC      SEMIS
;
HEADER({.R},{DOTR},{DOCOL})
        DC      TOR
        DC      STOD
        DC      FROMR
        DC      DDOTR
        DC      SEMIS
        PAGE
HEADER({D.},{DDOT},{DOCOL})
        DC      ZERO
        DC      DDOTR
        DC      SPACE
        DC      SEMIS
;
HEADER({.},{DOT},{DOCOL})
        DC      STOD
        DC      DDOT
        DC      SEMIS
;
HEADER({?},{QUES},{DOCOL})
        DC      FETCH
        DC      DOT
        DC      SEMIS
;
HEADER({U.},{UDOT},{DOCOL})
        DC      ZERO
        DC      DDOT
        DC      SEMIS
        PAGE
        ;{ At line     LINE ~4000}
HEADER({VLIST},{VLIST},{DOCOL})
        DC      LIT,80H
        DC      LOUT
        DC      STORE
        DC      CONT
        DC      FETCH
        DC      FETCH
VLIS1:  DC      LOUT    ;{BEGIN}
        DC      FETCH
        DC      CSLL
        DC      GREAT
        _0BRANCH(VLIS2) ;{IF}
        DC      CR
        DC      ZERO
        DC      LOUT
        DC      STORE   ;{ENDIF}
VLIS2:  DC      LDUP
        DC      IDDOT
        DC      SPACE
        DC      SPACE
        DC      PFA
        DC      LFA
        DC      FETCH
        DC      LDUP
        DC      ZEQU
        DC      QTERM
        DC      LOR
        _0BRANCH(VLIS1) ;{UNTIL}
        DC      DROP
        DC      SEMIS
;
_NORMAL_BYE_1_({
HEADER({BYE},{BYE},{DOCOL})
        DC      LIT, 4CH, ZERO, ZERO, ZERO, LIT, 21H, BIOS
;{ EXIT TO PC-DOS}
});_END___1__({ _NORMAL_BYE_1_})
_ABSOLUTELOAD_1_({
CODE_HEADER({BYE},{BYE})
;{ EXIT TO PC-DOS, if run from PC-DOS, otherwise hang or whatever.}
        _SWITCH_1_({JMPHERE_FROM_FORTH})
RETDOSV: JMP 0:0        ; {Filled in during boot}
        _BITS32_1_({SET_32_BIT_MODE})

});_END___1__({ _ABSOLUTELOAD_1_})
_HOSTED_LINUX_1_({
HEADER({BYE},{BYE},{DOCOL})
;{ Exit to linux, with okay status. }
        DC      ZERO, ZERO, ZERO, ONE, LINOS
});_END___1__({ _HOSTED_LINUX_1_})
;
worddoc({SCREEN},{LIST},{list},{n ---},{L0},{
Display the ascii text of screen forthvar({n}) on the selected output device.
forthcode({SCR}) contains the screen number during and after this process.},{})
HEADER({LIST},{LLIST},{DOCOL})
        DC      DECA
        DC      CR,LDUP
        DC      SCR,STORE
        DC      PDOTQ
        _STRING({SCR # })
        DC      DOT
        DC      LIT,10H
        DC      ZERO,XDO
LIST1:  DC      CR,IDO
        DC      LIT,3
        DC      DOTR,SPACE
        DC      IDO,SCR
        DC      FETCH,DLINE
        DC      QTERM   ;{ ?TERMINAL}
        _0BRANCH(LIST2)
        DC      LLEAV
LIST2:  _LOOP(LIST1)
        DC      CR,SEMIS
;
worddoc({SCREEN},{INDEX},{index},{from to --},{},{
Print the first line of each screen over the range forthvar({from}) , forthvar({to}) . This is
used to view the comment lines of an area of text on disc screens.},{{LIST}}} })
HEADER({INDEX},{INDEX},{DOCOL})
        DC      LIT,FF
        DC      EMIT,CR
        DC      ONEP,SWAP
        DC      XDO
INDE1:  DC      CR,IDO
        DC      LIT,3
        DC      DOTR,SPACE
        DC      ZERO,IDO
        DC      DLINE,QTERM
        _0BRANCH(INDE2)
        DC      LLEAV
INDE2:  _LOOP(INDE1)
        DC      SEMIS
;
worddoc({SCREEN},{TRIAD},{triad},{scr --},{},{
Display on the selected output device the three screens which
include that numbered forthvar({scr}) , beginning with a screen evenly divisible
by three. Output is suitable for source text records, and includes a
reference line at the bottom taken from line 15 of screen 4.},{{MESSAGE}})
HEADER({TRIAD},{TRIAD},{DOCOL})
        DC      LIT,FF
        DC      EMIT
        DC      LIT,3
        DC      SLASH
        DC      LIT,3
        DC      STAR
        DC      LIT,3
        DC      OVER,PLUS
        DC      SWAP,XDO
TRIA1:  DC      CR,IDO
        DC      LLIST
        DC      QTERM   ;{ ?TERMINAL}
        _0BRANCH(TRIA2)
        DC      LLEAV   ;{LEAVE}
TRIA2:  _LOOP(TRIA1)    ;{ENDIF}
        DC      CR
        DC      SEMIS
;
HEADER({.CPU},{DOTCPU},{DOCOL})
;{ PRINT CPU TYPE (8088)}
        DC      BASE,FETCH
        DC      LIT,36
        DC      BASE,STORE
        DC      LIT,CELLS(12),PORIG ;
        DC      TFET
_BITS32_1_({
        DC      LIT, 10000H, STAR, PLUS, ZERO
});_END___1__({ _BITS32_1_})
        DC      DDOT
        DC      BASE,STORE
        DC      SEMIS
        PAGE
_COMMENT

            CODE LEVEL "MATCH" DEFINITIONS

 STACK PARAMETERS:

 ( cursor:addr   byte:left   str:addr  str:len
           ---   flag  new:cursor:offset         )

 This version of MATCH will handle string lengths
      up to 65535 bytes in length.

_ENDCOMMENT
CODE_HEADER({MATCH},{MATCH})
        MOV     DI,SI   ;{ SAVE IP}
        POP     CX      ;{ STRING COUNT}
        POP     BX      ;{STRING ADDR}
        POP     DX      ;{BYTES LEFT TO SEARCH}
        POP     SI      ;{CURSOR ADDR}
        PUSH    SI      ;{SAVE COPY}
MAT1:   LODSB           ;{GET FIRST BYTE}
        CMP     AL,[BX] ;{MATCH?}
        JNZ     MAT3    ;{NO}
        PUSH    BX      ;{SAVE STRING ADDR}
        PUSH    CX      ;{ &   STRING COUNT}
        PUSH    SI      ;{ &   CURSOR ADDR}
;{ TRY TO MATCH REMAINING CHARS IN STRING}
;
MAT2:   DEC     CX      ;{STR. COUNT -1}
        JZ      MATCHOK ;{EXIT - MATCH FOUND}
        DEC     DX      ;{BYTES LEFT -1}
        JZ      NOMATCH ;{EXIT - NO MATCH}
        INC     BX      ;{NEXT STR CHAR ADDR}
        LODSB           ;{GET FIRST BYTE}
        CMP     AL,[BX] ;{MATCH?}
        JZ      MAT2    ;{YES, GET MORE}
;{ NO MATCH YET}
        POP     SI
        POP     CX
        POP     BX      ;{RESTORE POINTERS}
MAT3:   DEC     DX      ;{BYTE LEFT COUNT -1}
        JNZ     MAT1    ;{START OVER}
        JMP     MAT4    ;{EXIT...NO MATCH}
MATCHOK:
NOMATCH:
        POP     CX      ;{ADJUST STACK}
        POP     CX      ;{FOR EXIT}
        POP     CX
;{ EXIT HERE: DX = TRUE/FALSE FLAG ( 0=NO MATCH)}
;
MAT4:   MOV     AX,SI   ;{NEW CURSOR ADDR}
        POP     SI      ;{GET STARTING ADDR}
        SUB     AX,SI   ;{COMPUTE CURSOR OFFSET}
        MOV     SI,DI   ;{GET BACK UP}
        _2PUSH          ;{ BYE..BYE}
        PAGE
_DIRECTMAPPED_1_({
;
HEADER({LOW-DP},{LOWDP},{DOUSE})
        DC      CELLS(14)
;
HEADER({LOW-EM},{LOWEM},{DOUSE})
        DC      CELLS(15)
;
});_END___1__({ _DIRECTMAPPED_1_})
;
;{**** LAST DICTIONARY WORD ****}
HEADER({TASK},{TASK},{DOCOL})
        DC      SEMIS
;
_EQULAYOUT_1_({
TEXTEND  EQU     $       ; {Show end of dictionary.}
INITDP   EQU     M4_INITDP ;{Where we want new words.}
});_END___1__({ _EQULAYOUT_1_})

_COMMENT

The remaining memory ( up to 'EM' ) is
used for:

        1. EXTENSION DICTIONARY
        2. PARAMETER STACK
        3. TERMINAL INPUT BUFFER
        4. RETURN STACK
        5. USER VARIABLE AREA
        6. DISK BUFFERS (UNLESS REQURIED <1 MBYTE)


_ENDCOMMENT

_HOSTED_LINUX_1_({
;       This is the proper way to do it.
;       No memory addresses should be arrived at through equates.
;       However now we must teach the linker to keep the
;       two sections together.

FORTHSIZE       EQU     $-figforth
         section dictionary nobits write exec alloc

INITDP:                 ;  It may be that it is not consecutive with TASK
                        ;  And that is a hell of a problem.
        BUFFERSIZE      EQU  (KBBUF+2*CW)*NBUF

       _RESB    M4_EM - FORTHSIZE - RTS - US - BUFFERSIZE
INITS0:                         ;{ Growns down}
STRTIB: _RESB     RTS            ;{ Start return stack area}
INITR0:                         ;{ Grows down}
STRUSA: _RESB    US              ;{ User area}
BUF1:   _RESB    BUFFERSIZE      ;{ FIRST DISK BUFFER}
EM:
});_END___1__({ _HOSTED_LINUX_1_})
;

 CSEG    ENDS
        PAGE
_COMMENT

  MISC. NOTES AND SCATTERED THOUGHTS

- This source will assemble on all platforms where NASM is
  available by the command line:
  nasm -fbin fig86.asm -o fig86.com
  (There may be exceptions for special configurations.)
  The result will run on MSDOS systems only, or stand alone
  an an IBM-compatible computer

- In a MODERN version <ctrl> P  will echo all output to the
  printer. This is not programmed here, but a feature of the OS.
  The operating system may make available a command history too.

- Use the installation manual.  Descriptions for all FIG
  words are given.  Those ERROR messages you get in FORTH
  correspond to the relative line numbers in blocks
  4 and 5 of the installation manual's model.

- Remember that all the FORTH words in this version are
  upper case letters.  Use <CAPS LOCK> when in FORTH.

- Changing variable EM will allow you to create a larger
  dictionary space.  However I suggest you develop and
  DEBUG with EM set to 4000H.  Setting it to a larger value
  will result in a larger FORTH.EXE file, and you may
  need to run EXE2BIN ( Chap 10, DOS 2.0 ) to get enough
  disk space.  Once you are satisfied with what you have,
  then by all means take that extra memory.

- Reading the section on batch files may speed up your
  developement.  See the example files that came with
  the Macro Assembler.

- Subscribe to FORTH Dimensions.  It is a valuable source
  of system and application ideas.  Talking with fellow
  FORTH programmers is sure to stir up some exciting ideas.
  Consider joining a FIG chapter.  See the back of FORTH
  Dimensions for more info.

- <Ctrl-Break> will vector to WARM start ( Label WRM: )

_ENDCOMMENT

;{ Define the entry point, not valid for auto booting.}
        END     ORIG
